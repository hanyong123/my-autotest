*** Settings ***
Library           Selenium2Library
Resource          config.txt

*** Keywords ***
转到PPPoE接入设置页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#c_menu_wan > div.m_c_txt
    Wait Until Page Contains Element    id=conntype
    Select From List By Label    id=conntype    PPPoE
    Click Element    id=show_wan_advance

设置PPPoE接入参数
    [Arguments]    ${user}=hany    ${password}=969090    ${service_name}=${None}    ${ac_name}=${None}    ${mac}=${None}    ${mtu}=${None}
    ...    ${dns1}=${None}    ${dns2}=${None}    ${connect_mode}=1    ${outtime}=5
    Wait Until Keyword Succeeds    5    1    Element Should Be Visible    id=pppoe_username
    Input Text    id=pppoe_username    ${user}
    Input Password    id=pppoe_pwd    ${password}
    Run Keyword If    '${service_name}'!='${None}'    Input Text    id=pppoe_service_name    ${service_name}
    Run Keyword If    '${ac_name}'!='${None}'    Input Text    id=pppoe_ac_name    ${ac_name}
    Run Keyword If    '${mac}' != '${None}'    Input Text    id=mac_addr    ${mac}
    Run Keyword If    '${mtu}'!='${None}'    Input Text    id=pppoe_mtu    ${mtu}
    Run Keyword If    '${dns1}'!='${None}'    Input Text    id=dns_a    ${dns1}
    Run Keyword If    '${dns2}'!='${None}'    Input Text    id=dns_b    ${dns2}
    Run Keyword If    '${connect_mode}'=='1'    Click Element    id=ppp_connect_mode_auto
    Run Keyword If    '${connect_mode}'=='2'    Click Element    id=ppp_connect_mode_time
    Run Keyword If    '${connect_mode}'=='3'    Click Element    id=ppp_connect_mode_hand
    Run Keyword If    '${connect_mode}' == '2'    Input Text    id=ppp_time    ${outtime}
    Click Button    id=save_wan_set

弹出对话框
    [Arguments]    ${dlg_string}
    ${style}    Get Element Attribute    xpath=//div[@class="df_lock_load"]@style
    Should Contain    ${style}    display: block
    ${string}    Get Text    xpath=//div[@class="df_lock_load"]
    Should Contain    ${string}    ${dlg_string}

弹出对话框消失
    ${style}    Get Element Attribute    xpath=//div[@class="df_lock_load"]@style
    Should Contain    ${style}    display: none

设置成功
    Set Selenium Speed    0
    Wait Until Keyword Succeeds    5    0    弹出对话框    Saved Successfully!
    Wait Until Keyword Succeeds    15    5    弹出对话框消失

设置失败
    Set Selenium Speed    0
    Wait Until Keyword Succeeds    5    0    弹出设置失败对话框
    Wait Until Keyword Succeeds    15    5    弹出对话框消失

弹出设置失败对话框
    ${style}    Get Element Attribute    xpath=//div[@class="df_lock_load"]@style
    Should Contain    ${style}    display: block
    ${string}    Get Text    xpath=//div[@class="df_lock_load"]
    Should Not Be Empty    ${string}
    Should Not Contain    ${string}    Saving
    Should Not Contain    ${string}    Saved Successfully!

转到DHCP接入设置页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#c_menu_wan > div.m_c_txt
    Wait Until Page Contains Element    id=conntype
    Select From List By Label    id=conntype    DHCP(Cable Modem)
    Click Element    id=show_wan_advance

设置DHCP接入参数
    [Arguments]    ${mac}=${None}    ${mtu}=${None}    ${dns1}=${None}    ${dns2}=${None}
    Run Keyword If    '${mac}'!='${None}'    Input Text    id=mac_addr    ${mac}
    Run Keyword If    '${mtu}'!='${None}'    Input Text    id=dhcp_mtu    ${mtu}
    Run Keyword If    '${dns1}'!='${None}'    Input Text    id=dns_a    ${dns1}
    Run Keyword If    '${dns2}'!='${None}'    Input Text    id=dns_b    ${dns2}
    Click Button    id=save_wan_set

打开浏览器
    Open Browser    ${base_url}    ${browser}

关闭浏览器
    Close Browser

显示消息
    [Arguments]    ${message}
    Set Selenium Speed    0
    Wait Until Keyword Succeeds    5    0    弹出对话框    ${message}
    Wait Until Keyword Succeeds    15    5    弹出对话框消失

验证输入框最大输入长度
    [Arguments]    ${locator}    ${content}    ${max_len}
    Input Text    ${locator}    ${content}
    ${v}    Get Value    ${locator}
    Length Should Be    ${v}    ${max_len}

转到静态接入设置页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#c_menu_wan > div.m_c_txt
    Wait Until Page Contains Element    id=conntype
    Select From List By Label    id=conntype    Static IP
    Click Element    id=show_wan_advance

设置静态接入参数
    [Arguments]    ${wan_static_ip}    ${wan_static_mask}    ${wan_static_gw}    ${mac}=${None}    ${mtu}=${None}    ${dns1}=${None}
    ...    ${dns2}=${None}
    Input Text    id=wan_ip    ${wan_static_ip}
    Input Text    id=wan_mask    ${wan_static_mask}
    Input Text    id=wan_gw    ${wan_static_gw}
    Run Keyword If    '${mac}'!='${None}'    Input Text    id=mac_addr    ${mac}
    Run Keyword If    '${mtu}'!='${None}'    Input Text    id=static_mtu    ${mtu}
    Run Keyword If    '${dns1}'!='${None}'    Input Text    id=dns_a    ${dns1}
    Run Keyword If    '${dns2}'!='${None}'    Input Text    id=dns_b    ${dns2}
    Click Button    id=save_wan_set

转到内网配置页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#p_menu_network > div.m_p_txt
    Click Element    id=id=c_menu_lan

设置LAN口MAC
    [Arguments]    ${mac}
    Wait Until Page Contains Element    id=lan_mac
    Input Text    id=lan_mac    ${mac}
    Click Button    id=save_lan_mac

设置LAN口IP和MASK
    [Arguments]    ${ip}    ${mask}
    Wait Until Page Contains Element    id=lan_ip
    Input Text    id=lan_ip    ${ip}
    Input Text    id=lan_mask    ${mask}
    Click Button    id=save_lan_set

设置DHCP服务器
    [Arguments]    ${start_ip}    ${end_ip}    ${enable}=1
    转到内网配置页面
    Run Keyword If    '${enable}'==1    Click Element    id=dhcp_enable_on
    Run Keyword If    '${enable}'==0    Click Element    id=dhcp_enable_off
    Run Keyword If    '${enable}'==1    Input Text    id=dhcp_start_ip    ${start_ip}
    Run Keyword If    '${enable}'==1    Input Text    id=dhcp_end_ip    ${end_ip}
    Click Button    id=save_dhcp_server

设置DHCP服务器保留地址
    [Arguments]    ${des}    ${ip}    ${mac}
    转到DHCP保留地址设置页面
    Input Text    id=descrip    ${des}
    Input Text    id=reserve_ip    ${ip}
    Input Text    id=reserve_mac    ${mac}
    Click Button    id=add_reservation

转到DHCP保留地址设置页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#p_menu_network > div.m_p_txt
    Click Element    css=#c_menu_address_reservation > div.m_c_txt

转到IP/MAC绑定页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_advanced > div.m_p_txt
    Click Element    css=#p_menu_advanced > div.m_p_txt
    Click Element    css=#c_menu_binds > div.m_c_txt

设置IP/MAC绑定
    [Arguments]    ${des}    ${ip}    ${mac}    ${interface}=lan
    Input Text    id=binds_des    ${des}
    Input Text    id=binds_ip    ${ip}
    Input Text    id=binds_mac    ${mac}
    Run Keyword If    '${interface}'=='lan'    Select From List By Value    id=binds_port    0
    Run Keyword If    '${interface}'=='wan'    Select From List By Value    id=binds_port    1
    Click Element    id=add_binds

转到IP过滤页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_management > div.m_p_txt
    Click Element    css=#p_menu_management > div.m_p_txt
    Click Element    css=#c_menu_ip_filter > div.m_c_txt

设置IP过滤
    [Arguments]    ${des}=${None}    ${rule}=1    ${shost}=all    ${sip}=${None}    ${smask}=${None}    ${start_sip}=${None}
    ...    ${end_sip}=${None}    ${dhost}=all    ${dip}=${None}    ${dmask}=${None}    ${start_dip}=${None}    ${end_dip}=${None}
    ...    ${protcol}=ALL    ${start_port}=${None}    ${end_port}=${None}    ${day_locator}=${None}    ${start_time}=0    ${end_time}=0
    ...    ${allday_locator}=${None}
    Run Keyword If    '${des}'!='${None}'    Input text    id=ip_describe    ${des}
    Select From List By Value    id=ip_rule    ${rule}
    Select From List By Value    id=ip_src_sele    ${shost}
    Run Keyword If    '${sip}'!='${None}'    Input text    id=ip_src_ip    ${sip}
    Run Keyword If    '${smask}'!='${None}'    Input text    id=ip_src_mask    ${smask}
    Run Keyword If    '${start_sip}'!='${None}'    Input text    id=ip_src_start    ${start_sip}
    Run Keyword If    '${end_sip}'!='${None}'    Input text    id=ip_src_end    ${end_sip}
    Select From List By Value    id=ip_des_sele    ${dhost}
    Run Keyword If    '${dip}'!='${None}'    Input text    id=ip_des_ip    ${dip}
    Run Keyword If    '${dmask}'!='${None}'    Input text    id=ip_des_mask    ${dmask}
    Run Keyword If    '${start_dip}'!='${None}'    Input text    id=ip_des_start    ${start_dip}
    Run Keyword If    '${end_dip}'!='${None}'    Input text    id=ip_des_end    ${end_dip}
    Select From List By Value    id=ip_proto    ${protcol}
    Run Keyword If    '${start_port}'!='${None}'    Input text    id=ip_port_start    ${start_port}
    Run Keyword If    '${end_port}'!='${None}'    Input text    id=ip_port_end    ${end_port}
    @{day_locator}    Get Variable Value    ${day_locator}
    :FOR    ${i}    IN    @{day_locator}
    \    Run Keyword If    '${i}'!='${None}'    Click Element    ${i}
    Select From List By Value    id=start    ${start_time}
    Select From List By Value    id=end    ${end_time}
    Run Keyword If    '${allday_locator}'!='${None}'    Click Element    ${allday_locator}
    Click Element    id=add_ip_filter

转到MAC过滤页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_management > div.m_p_txt
    Click Element    css=#p_menu_management > div.m_p_txt
    Click Element    css=#c_menu_mac_filter > div.m_c_txt

设置MAC过滤
    [Arguments]    ${des}=${None}    ${rule}=1    ${mac}=${None}    ${day_locator}=${None}    ${start_time}=0    ${end_time}=0
    ...    ${allday_locator}=${None}
    Run Keyword If    '${des}'!='${None}'    Input text    id=mac_describe    ${des}
    Select From List By Value    id=mac_rule    ${rule}
    Run Keyword If    '${mac}'!='${None}'    Input text    id=mac_filter    ${mac}
    @{day_locator}    Get Variable Value    ${day_locator}
    :FOR    ${i}    IN    @{day_locator}
    \    Click Element    ${i}
    Select From List By Value    id=start    ${start_time}
    Select From List By Value    id=end    ${end_time}
    Run Keyword If    '${allday_locator}'!='${None}'    Click Element    ${allday_locator}
    Click Element    id=add_mac_filter

转到恢复缺省页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=#p_menu_misc > div.m_p_txt
    Click Element    css=#p_menu_misc > div.m_p_txt
    Click Element    css=#c_menu_default > div.m_c_txt

恢复出厂设置
    转到恢复缺省页面
    Click Element    id=default_set
    Choose Ok On Next Confirmation
    ${message}    Confirm Action

系统重启
    Wait Until Keyword Succeeds    15    5    Page Should Contain Element    id=lock_bg
    ${style}    Get Element Attribute    lock_bg@style
    Should Contain    ${style}    display: block

等待系统重启完成
    Wait Until Keyword Succeeds    60    5    Page Should Not Contain Element    id=lock_bg

转到接口状态页面
    Set Selenium Speed    .5
    Wait Until Page Contains Element    css=div.m_p_txt
    Click Element    css=div.m_p_txt

WAN口IP是${ip}
    ${s}    Get Text    xpath=//div[@id="content_layer"]/div[1]/div[2]/div[3]/span
    Should Be Equal    ${s}    ${ip}
