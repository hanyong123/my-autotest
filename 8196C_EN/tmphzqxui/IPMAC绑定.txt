*** Settings ***
Suite Setup       Run Keywords    打开浏览器    AND    转到IP/MAC绑定页面
Suite Teardown    关闭浏览器
Resource          keyword.txt

*** Variables ***
@{ip}             192.168.1.66    192.168.1.67    192.168.1.68    192.168.1.69    192.168.1.70    192.168.1.71    192.168.1.72
...               192.168.1.73    192.168.1.10    192.168.1.11    192.168.1.12    192.168.1.13    192.168.1.14    192.168.1.15
...               192.168.1.16    192.168.1.17    192.168.1.18    192.168.1.19    192.168.1.20    192.168.1.21    192.168.1.22
...               192.168.1.23    192.168.1.24    192.168.1.25    192.168.1.26    192.168.1.27    192.168.1.28    192.168.1.29
...               192.168.1.30    192.168.1.31    192.168.1.32    192.168.1.33    192.168.1.34    192.168.1.35    192.168.1.36
...               192.168.1.37    192.168.1.38    192.168.1.39    192.168.1.40    192.168.1.41    192.168.1.42    192.168.1.43
...               192.168.1.44    192.168.1.45    192.168.1.46    192.168.1.47    192.168.1.48    192.168.1.49    192.168.1.50
...               192.168.1.51    192.168.1.52    192.168.1.53    192.168.1.54    192.168.1.55    192.168.1.56    192.168.1.57
...               192.168.1.58    192.168.1.59    192.168.1.60    192.168.1.61    192.168.1.62    192.168.1.63    192.168.1.64
...               192.168.1.65
@{mac}            00:1c:c0:a2:c3:00    00:1c:c0:a2:c3:01    00:1c:c0:a2:c3:02    00:1c:c0:a2:c3:03    00:1c:c0:a2:c3:04    00:1c:c0:a2:c3:05    00:1c:c0:a2:c3:23
...               00:1c:c0:a2:c3:06    00:1c:c0:a2:c3:07    00:1c:c0:a2:c3:08    00:1c:c0:a2:c3:09    00:1c:c0:a2:c3:0a    00:1c:c0:a2:c3:0b    00:1c:c0:a2:c3:0c
...               00:1c:c0:a2:c3:0d    00:1c:c0:a2:c3:0e    00:1c:c0:a2:c3:0f    00:1c:c0:a2:c3:10    00:1c:c0:a2:c3:11    00:1c:c0:a2:c3:12    00:1c:c0:a2:c3:13
...               00:1c:c0:a2:c3:14    00:1c:c0:a2:c3:15    00:1c:c0:a2:c3:16    00:1c:c0:a2:c3:17    00:1c:c0:a2:c3:18    00:1c:c0:a2:c3:19    00:1c:c0:a2:c3:1a
...               00:1c:c0:a2:c3:1b    00:1c:c0:a2:c3:1c    00:1c:c0:a2:c3:1d    00:1c:c0:a2:c3:1e    00:1c:c0:a2:c3:2c    00:1c:c0:a2:c3:20    00:1c:c0:a2:c3:21
...               00:1c:c0:a2:c3:22    00:1c:c0:a2:c3:24    00:1c:c0:a2:c3:25    00:1c:c0:a2:c3:26    00:1c:c0:a2:c3:27    00:1c:c0:a2:c3:28    00:1c:c0:a2:c3:29
...               00:1c:c0:a2:c3:2a    00:1c:c0:a2:c3:2b    00:1c:c0:a2:c3:1f    00:1c:c0:a2:c3:2d    00:1c:c0:a2:c3:2e    00:1c:c0:a2:c3:2f    00:1c:c0:a2:c3:30
...               00:1c:c0:a2:c3:31    00:1c:c0:a2:c3:32    00:1c:c0:a2:c3:33    00:1c:c0:a2:c3:34    00:1c:c0:a2:c3:35    00:1c:c0:a2:c3:36    00:1c:c0:a2:c3:37
...               00:1c:c0:a2:c3:38    00:1c:c0:a2:c3:39    00:1c:c0:a2:c3:3a    00:1c:c0:a2:c3:3b    00:1c:c0:a2:c3:3c    00:1c:c0:a2:c3:3d    00:1c:c0:a2:c3:3e
...               00:1c:c0:a2:c3:3f

*** Test Cases ***
IpMacBinding_IP_test_1
    [Documentation]    IP地址输入框超过15个字符后，无法输入
    验证IPMAC绑定IP地址最大输入长度    192.168.111.111222222    15

IpMacBinding_IP_test_2
    [Documentation]    IP地址输入框：小数点>3，提示IP长度不正确
    验证IPMAC绑定    ${EMPTY}    192.168.1.1.1    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_3
    [Documentation]    IP地址输入框：小数点<3，提示IP长度不正确
    验证IPMAC绑定    ${EMPTY}    192.168.1    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_4
    [Documentation]    IP地址不能为空
    验证IPMAC绑定    ${EMPTY}    ${EMPTY}    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_5
    [Documentation]    IP地址输入框第一段IP值超出范围，提示只能在0~255之间
    [Template]    验证IPMAC绑定
    ${EMPTY}    -1.168.1.5    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    256.168.1.5    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_6
    [Documentation]    IP地址输入框第二段IP值超出范围，提示只能在0~255之间
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.-1.1.5    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.256.1.5    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_7
    [Documentation]    IP地址输入框第三段IP值超出范围，提示只能在0~255之间
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.-1.6    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.256.8    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_8
    [Documentation]    IP地址输入框第四段IP值超出范围，提示只能在0~255之间
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.-1    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.256    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_9
    [Documentation]    IP地址输入框存在非法字符，提示发现n个非法字符
    [Template]    验证IPMAC绑定
    ${EMPTY}    19*.168.1.8    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.16#.1.7    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1'.5    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.9"    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.5\\    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.5/    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.3?    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1’.12    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.23”    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.11、    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.14：    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_10
    [Documentation]    IP地址第一段、第二段、第三段、第四段均不能为空
    [Template]    验证IPMAC绑定
    ${EMPTY}    168.1.6    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192. .1.6    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168. .5    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_11
    [Documentation]    IP地址输入0.0.0.0，提示IP不能为全0
    验证IPMAC绑定    ${EMPTY}    0.0.0.0    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_12
    [Documentation]    IP地址不为组播地址：223 < n1 < 240
    [Template]    验证IPMAC绑定
    ${EMPTY}    224.168.1.4    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    225.168.1.4    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    227.168.1.4    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    230.168.1.4    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    239.168.1.4    00:1c:c0:a2:c3:80    ${common_ip_addr_err}
    ${EMPTY}    240.168.1.4    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_13
    [Documentation]    IP地址不为回环地址：n1 = 127
    验证IPMAC绑定    ${EMPTY}    127.168.1.6    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_IP_test_14
    [Documentation]    IP地址不为保留地址：n1=0
    验证IPMAC绑定    ${EMPTY}    0.168.1.4    00:1c:c0:a2:c3:80    ${common_ip_addr_err}

IpMacBinding_MAC_test_15
    [Documentation]    MAC配置输入框不能为空
    验证IPMAC绑定    ${EMPTY}    192.168.1.7    ${EMPTY}    ${common_mac_addr_err}

IpMacBinding_MAC_test_16
    [Documentation]    MAC配置输入框超出17个字符后无法输入
    验证IPMAC绑定MAC地址最大输入长度    00:1c:c0:a2:c3:8034343    17

IpMacBinding_MAC_test_17
    [Documentation]    MAC配置输入框长度不满17个字符，提示格式错误
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.6    00:1c:c0:a2:c3:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:1c:c0:a2:c:80    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:1c:c0:a:c3:80    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:1c:c:a2:c3:80    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:c:c0:a2:c3:80    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    0:1c:c0:a2:c3:80    ${common_mac_addr_err}

IpMacBinding_MAC_test_18
    [Documentation]    MAC地址的分隔符不全为-时，提示格式错误
    验证IPMAC绑定    ${EMPTY}    192.168.1.7    00-1c-c0-a2-c3:87    ${common_mac_addr_err}

IpMacBinding_MAC_test_19
    [Documentation]    MAC地址的分隔符不全为:时，提示格式错误
    验证IPMAC绑定    ${EMPTY}    192.168.1.7    00:1c:c0:a2:c3-87    ${common_mac_addr_err}

IpMacBinding_MAC_test_20
    [Documentation]    MAC地址的分隔符个数不等于5，提示错误
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.6    00:1c:c0:a2:c3    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:1c:c0:a:c3:8:1    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3-9-3    ${common_mac_addr_err}

IpMacBinding_MAC_test_21
    [Documentation]    MAC地址的内容超出00~FF的范围时，提示格式错误
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.6    fg:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    gf-1c-c0-a2-c3-87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:fg:c0:a:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-fg-a2-c3-95    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:ac:c0:fg:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-fg-95    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3-fg    ${common_mac_addr_err}

IpMacBinding_MAC_test_22
    [Documentation]    MAC地址中的内容有不属于｛0-9，A-F，a-f｝范围的，提示格式错误
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.6    0*:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1(-c0-a2-c3-87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:00:^0:a:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-00-a#-c3-95    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:ac:c0:00:c':87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3-8"    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3-8/    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:ac:c0:00:c’:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3-8”    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3-8。    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-c0-a2-c3-8、    ${common_mac_addr_err}

IpMacBinding_MAC_test_23
    [Documentation]    MAC配置输入框以xx-xx-xx-xx-xx-xx格式输入MAC地址，xx不能为空
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.6    :1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00- -c0-a2-c3-87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:00: :a:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-00- -c3-95    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:ac:c0:00: :87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:ac:c0:00:c3:    ${common_mac_addr_err}

IpMacBinding_MAC_test_24
    [Documentation]    MAC配置输入框以xx-xx-xx-xx-xx-xx格式输入MAC地址，xx不能大于2字节
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.6    123:1c:c0:a2:c3:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-123-c0-a2-c3-8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:00:134 :a:c3:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00-1c-00-345-c3-9    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:ac:c0:00:533:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.6    00:ac:c0:00:c:657    ${common_mac_addr_err}

IpMacBinding_MAC_test_25
    [Documentation]    MAC地址为全0时 00-00-00-00-00-00，提示格式错误
    验证IPMAC绑定    ${EMPTY}    192.168.1.7    00-00-00-00-00-00    ${common_mac_addr_err}

IpMacBinding_MAC_test_26
    [Documentation]    MAC地址为组播地址时 xn-xx-xx-xx-xx-xx（n属于[1,3,5,7,9,B,D,F]），提示格式错误
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.7    ff-ff-ff-ff-ff-ff    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    FF-FF-FF-FF-FF-FF    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    ff:ff:ff:ff:ff:ff    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    FF:FF:FF:FF:FF:FF    ${common_mac_addr_err}

IpMacBinding_MAC_test_27
    [Documentation]    MAC地址为组播地址时 xn-xx-xx-xx-xx-xx（n属于[1,3,5,7,9,B,D,F]），提示格式错误
    [Template]    验证IPMAC绑定
    ${EMPTY}    192.168.1.7    31:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    33:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    35:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    37:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    39:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    3B:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    3D:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    3F:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    3b:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    3f:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.7    3d:1c:c0:a2:c3:87    ${common_mac_addr_err}

IpMacBinding_MAC_test_28
    [Documentation]    IP/MAC地址符合格式规范，成功保存并生效
    [Template]
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    ${EMPTY}    192.168.1.7    32:1c:c0:a2:c3:90    ${common_unlock_add}
    验证IPMAC绑定    ${EMPTY}    172.168.1.31    36:1c:c0:a2:c3:90    ${common_unlock_add}
    验证IPMAC绑定    ${EMPTY}    10.0.0.9    34:1c:c0:a2:c3:99    ${common_unlock_add}
    删除所有IPMAC绑定条目

IpMacBinding_Descip_test_29
    [Documentation]    规则名称允许为空，为空时能够保存成功；规则列表中展示出的该条规则的规则名为空
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    ${EMPTY}    192.168.1.77    00:1c:c0:a2:c4:85    ${common_unlock_add}
    IPMAC绑定列表第1行第2列是${EMPTY}
    删除所有IPMAC绑定条目

IpMacBinding_Descip_test_30
    [Documentation]    规则名称输入数据带有 '"\或空格 ，提示“ 不能含有特殊字符 \ '"\和空格 ”
    [Template]    验证IPMAC绑定
    34'    192.168.1.2    32:1c:c0:a2:c3:87    ${common_string_err}
    34"    192.168.1.3    34:1c:c0:a2:c3:87    ${common_string_err}
    34\\    192.168.1.4    36:1c:c0:a2:c3:87    ${common_string_err}
    3 4    192.168.1.4    36:1c:c0:a2:c3:87    ${common_string_err}

IpMacBinding_Descip_test_31
    [Documentation]    规则名长度最大31个字符，超过31字符无法输入
    验证IPMAC绑定规则名字最大输入长度    12345678912345678912345678912345678    31

IpMacBinding_Descip_test_32
    [Documentation]    规则名输入30个字符，再输入一个中文，导致超过31个字符，提示“规则名过长，已经自动截断”。
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    012345678901234567890123456789我    192.168.1.33    00:1c:c0:a2:c4:aa    ${common_unlock_add}
    IPMAC绑定列表第1行第2列是012345678901234567890123456789
    删除所有IPMAC绑定条目

IpMacBinding_Add_test_33
    [Documentation]    输入正确的IP地址、MAC地址、规则名，点击“增加”按钮；提示“操作成功！”，并刷新列表
    [Template]
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    11    192.168.1.44    00:1c:c0:a2:c4:aa    ${common_unlock_add}
    验证IPMAC绑定    45    172.23.43.67    00:1c:c0:a2:c4:80    ${common_unlock_add}
    验证IPMAC绑定列表某一行内容    1    45    172.23.43.67    00:1c:c0:a2:c4:80
    验证IPMAC绑定列表某一行内容    2    11    192.168.1.44    00:1c:c0:a2:c4:aa
    删除所有IPMAC绑定条目

IpMacBinding_Add_test_34
    [Documentation]    当已添加512条条目时，继续添加第513条条目，提示“添加失败，已达到最大条目数！”
    删除所有IPMAC绑定条目
    Sleep    5
    : FOR    ${i}    IN RANGE    14
    \    验证IPMAC绑定    ${EMPTY}    @{ip}[${i}]    @{mac}[${i}]    ${common_unlock_add}
    验证IPMAC绑定    ${EMPTY}    @{ip}[${i}]    @{mac}[${i}]    ${common_unlock_add}

IpMacBinding_Add_test_35
    [Documentation]    添加条目，IP地址和已有条目的地址重复，提示已有相同IP/MAC绑定条目
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    65    192.168.1.5    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证IPMAC绑定    87    192.168.1.5    36:1c:c0:a2:c3:87    ${common_ip_occupy_err}
    删除所有IPMAC绑定条目

IpMacBinding_Add_test_36
    [Documentation]    添加条目，MAC地址和已有条目的地址重复，提示已有相同IP/MAC绑定条目
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    65    192.168.1.6    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证IPMAC绑定    87    192.168.1.9    36:1c:c0:a2:c3:88    ${common_mac_occupy_err}
    删除所有IPMAC绑定条目

IpMacBinding_Modify_test_37
    [Documentation]    成功修改一条IP/MAC绑定条目
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    65    192.168.1.6    36:1c:c0:a2:c3:88    ${common_unlock_add}
    修改IPMAC绑定列表里第1行规则名为jjjIP地址为192.168.1.55MAC地址为36:1c:c0:a2:c3:80并显示消息${common_unlock_add}
    验证IPMAC绑定列表某一行内容    1    jjj    192.168.1.55    36:1c:c0:a2:c3:80
    删除所有IPMAC绑定条目

IpMacBinding_Modify_test_38
    [Documentation]    修改条目，IP地址和已有条目的地址重复，提示已有相同IP/MAC绑定条目
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    aa    192.168.1.99    36:1c:c0:a2:c3:82    ${common_unlock_add}
    验证IPMAC绑定    bb    192.168.1.88    36:1c:c0:a2:c3:83    ${common_unlock_add}
    修改IPMAC绑定列表里第1行规则名为jjjIP地址为192.168.1.99MAC地址为36:1c:c0:a2:c3:80并显示消息${common_ip_occupy_err}
    删除所有IPMAC绑定条目

IpMacBinding_Modify_test_39
    [Documentation]    修改条目，MAC地址和已有条目的地址重复，提示已有相同IP/MAC绑定条目
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    88    192.168.1.99    36:1c:c0:a2:c3:82    ${common_unlock_add}
    验证IPMAC绑定    99    192.168.1.88    36:1c:c0:a2:c3:83    ${common_unlock_add}
    修改IPMAC绑定列表里第1行规则名为jjjIP地址为192.168.1.77MAC地址为36:1c:c0:a2:c3:82并显示消息${common_mac_occupy_err}
    删除所有IPMAC绑定条目

IpMacBinding_Delete_test_41
    [Documentation]    点击条目对应的删除按钮，成功删除一条IP/MAC绑定条目，提示“操作成功！”，并刷新列表
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    aa    192.168.1.3    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证IPMAC绑定    bb    192.168.1.4    36:1c:c0:a2:c3:87    ${common_unlock_add}
    删除IPMAC绑定列表第1行并显示成功消息
    验证IPMAC绑定列表某一行内容是否删除成功    1    bb    192.168.1.4    36:1c:c0:a2:c3:87
    删除所有IPMAC绑定条目

IpMacBinding_Delete_test_42
    [Documentation]    点击删除全部按钮，成功删除所有IP/MAC绑定条目，提示“操作成功！”，并刷新列表
    删除所有IPMAC绑定条目
    Sleep    3
    验证IPMAC绑定    aa    192.168.1.3    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证IPMAC绑定    bb    192.168.1.4    36:1c:c0:a2:c3:87    ${common_unlock_add}
    验证IPMAC绑定    cc    192.168.1.5    36:1c:c0:a2:c3:86    ${common_unlock_add}
    删除所有IPMAC绑定条目
    IPMAC绑定列表里没有aa
    IPMAC绑定列表里没有192.168.1.3
    IPMAC绑定列表里没有36:1c:c0:a2:c3:88
    IPMAC绑定列表里没有bb
    IPMAC绑定列表里没有192.168.1.4
    IPMAC绑定列表里没有36:1c:c0:a2:c3:87
    IPMAC绑定列表里没有cc
    IPMAC绑定列表里没有192.168.1.5
    IPMAC绑定列表里没有36:1c:c0:a2:c3:86

*** Keywords ***
验证IPMAC绑定
    [Arguments]    ${des}    ${ip}    ${mac}    ${message}
    设置IP/MAC绑定    ${des}    ${ip}    ${mac}
    显示消息    ${message}

验证IPMAC绑定IP地址最大输入长度
    [Arguments]    ${ip}    ${max_len}
    验证输入框最大输入长度    id=binds_ip    ${ip}    ${max_len}

验证IPMAC绑定MAC地址最大输入长度
    [Arguments]    ${mac}    ${max_len}
    验证输入框最大输入长度    id=binds_mac    ${mac}    ${max_len}

验证IPMAC绑定规则名字最大输入长度
    [Arguments]    ${name}    ${max_len}
    验证输入框最大输入长度    id=binds_des    ${name}    ${max_len}

IPMAC绑定列表里含有${string}
    Table Should Contain    xpath=//table[@class="df_tab"]    ${string}

IPMAC绑定列表第${row}行第${col}列是${string}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    ${col}
    Should Be Equal    ${v}    ${string}

删除IPMAC绑定列表第${row}行并显示成功消息
    Click Element    xpath=//div[@class="tab_del_btn"][${row}]
    ${message}    Confirm Action
    Should Be Equal    ${message}    ${common_del_one_tip}
    Wait Until Keyword Succeeds    5    0    弹出对话框    ${common_unlock_delete}
    Wait Until Keyword Succeeds    5    1    弹出对话框消失

修改IPMAC绑定列表里第${row}行规则名为${name}IP地址为${ip}MAC地址为${mac}并显示消息${message}
    Click Element    xpath=(//div[@class="tab_mod_btn"])[${row}]
    Sleep    2
    Input Text    id=binds_des    ${name}
    Input Text    id=binds_ip    ${ip}
    Input Text    id=binds_mac    ${mac}
    Click Button    id=add_binds
    显示消息    ${message}

删除所有IPMAC绑定条目
    Click Element    xpath=(//input[@id='del_binds_all'])
    ${message}    Confirm Action
    Should Be Equal    ${message}    ${common_del_tip}
    Wait Until Keyword Succeeds    5    0    弹出对话框    ${common_unlock_delete}
    Wait Until Keyword Succeeds    5    1    弹出对话框消失

IPMAC绑定列表里没有${string}
    Run Keyword And Expect Error    *    Table Should Contain    xpath=//table[@class="df_tab"]    ${string}

判断IP和MAC是否保存成功
    [Arguments]    ${des}    ${ip}    ${mac}    ${interface}
    设置IP/MAC绑定    ${des}    ${ip}    ${mac}    ${interface}
    显示消息    ${common_unlock_add}
    Sleep    5
    ${ipVal}    Get Value    id=binds_ip
    Should Be Equal    ${ipVal}    ${ip}
    ${macVal}    Get Value    id=binds_mac
    Should Be Equal    ${macVal}    ${mac}
    Sleep    4

验证IPMAC绑定列表某一行内容
    [Arguments]    ${row}    ${des}    ${ip}    ${mac}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    2
    Should Be Equal    ${v}    ${des}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    3
    Should Be Equal    ${v}    ${ip}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    4
    Should Be Equal    ${v}    ${mac}

验证IPMAC绑定列表某一行内容是否删除成功
    [Arguments]    ${row}    ${des}    ${ip}    ${mac}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    2
    Should Not Be Equal    ${v}    ${des}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    3
    Should Not Be Equal    ${v}    ${ip}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    4
    Should Not Be Equal    ${v}    ${mac}

验证是否全部删除成功
    Should Be Empty    xpath=//table[@class="df_tab"]
