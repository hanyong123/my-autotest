*** Settings ***
Suite Setup       打开浏览器
Suite Teardown    关闭浏览器
Resource          keyword.txt

*** Variables ***
@{error_mtu_string}    啊    A    %    0900    a    ${EMPTY}    *

*** Test Cases ***
wan_mtuConfig_test_1
    Run Keyword And Continue On Failure    验证DHCP接入MTU    ${EMPTY}    ${common_dhcp_mtu_err}
    Run Keyword And Continue On Failure    验证静态接入MTU    ${EMPTY}    ${common_static_mtu_err}
    Run Keyword And Continue On Failure    验证PPPOE接入MTU    ${EMPTY}    ${common_pppoe_mtu_err}
    Run Keyword And Continue On Failure    验证L2TP接入MTU    ${EMPTY}    ${common_unlock_error}
    Run Keyword And Continue On Failure    验证PPTP接入MTU    ${EMPTY}    ${common_unlock_error}

wan_mtuConfig_test_2
    Run Keyword And Continue On Failure    验证DHCP接入MTU最大输入长度    12345    4
    Run Keyword And Continue On Failure    验证PPPOE接入MTU最大输入长度    12345    4
    Run Keyword And Continue On Failure    验证静态接入MTU最大输入长度    12345    4
    Run Keyword And Continue On Failure    验证L2TP接入MTU最大输入长度    12345    4
    Run Keyword And Continue On Failure    验证PPTP接入MTU最大输入长度    12345    4

wan_mtuConfig_test_3
    [Template]    验证静态接入MTU
    575    ${common_static_mtu_err}
    1501    ${common_static_mtu_err}
    1000    ${common_unlock_save}
    576     ${common_unlock_save}
    1500    ${common_unlock_save}

wan_mtuConfig_test_4
    [Template]    验证DHCP接入MTU
    575    ${common_dhcp_mtu_err}
    1501    ${common_dhcp_mtu_err}
    1000    ${common_unlock_save}
    576     ${common_unlock_save}
    1500    ${common_unlock_save}

wan_mtuConfig_test_5
    [Template]    验证PPPOE接入MTU
    575    ${common_pppoe_mtu_err}
    1493    ${common_pppoe_mtu_err}
    1000    ${common_unlock_save}
    576     ${common_unlock_save}
    1492    ${common_unlock_save}

wan_mtuConfig_test_6
    [Template]    验证PPTP接入MTU
    575    ${common_unlock_error}
    1441    ${common_unlock_error}
    1000    ${common_unlock_save}
    576    ${common_unlock_save}
    1440    ${common_unlock_save}

wan_mtuConfig_test_7
    [Template]    验证L2TP接入MTU
    575    ${common_unlock_error}
    1441    ${common_unlock_error}
    1000    ${common_unlock_save}
    576     ${common_unlock_save}
    1440    ${common_unlock_save}

wan_mtuConfig_test_8
    [Template]
    :FOR    ${mtu}    IN    @{error_mtu_string}
    \    Run Keyword And Continue On Failure    验证DHCP接入MTU    ${mtu}    ${common_dhcp_mtu_err}
    \    Run Keyword And Continue On Failure    验证PPPOE接入MTU    ${mtu}    ${common_pppoe_mtu_err}
    \    Run Keyword And Continue On Failure    验证静态接入MTU    ${mtu}    ${common_static_mtu_err}
    \    Run Keyword And Continue On Failure    验证L2TP接入MTU    ${mtu}    ${common_unlock_error}
    \    Run Keyword And Continue On Failure    验证PPTP接入MTU    ${mtu}    ${common_unlock_error}

*** Keywords ***
验证DHCP接入MTU
    [Arguments]    ${mtu}    ${message}
    转到DHCP接入设置页面
    设置DHCP接入参数    mtu=${mtu}
    显示消息    ${message}
    ${rmessage}    process_special_charators    ${message}
    Run Keyword If    '${rmessage}'=='${common_unlock_save}'    检查DHCP接入MTU的值是${mtu}

验证PPPOE接入MTU
    [Arguments]    ${mtu}    ${message}
    转到PPPoE接入设置页面
    设置PPPoE接入参数    mtu=${mtu}
    显示消息    ${message}
    ${rmessage}    process_special_charators    ${message}
    Run Keyword If    '${rmessage}'=='${common_unlock_save}'    检查PPPOE接入MTU的值是${mtu}

验证静态接入MTU
    [Arguments]    ${mtu}    ${message}
    转到静态接入设置页面
    设置静态接入参数    ${router_wan_ip}    ${router_wan_mask}    ${host_wan_ip}    mtu=${mtu}    dns1=9.9.9.9    dns2=8.8.8.8
    显示消息    ${message}
    ${rmessage}    process_special_charators    ${message}
    Run Keyword If    '${rmessage}'=='${common_unlock_save}'    检查静态接入MTU的值是${mtu}

验证DHCP接入MTU最大输入长度
    [Arguments]    ${mtu}    ${max_len}
    转到DHCP接入设置页面
    验证输入框最大输入长度    id=dhcp_mtu    ${mtu}    ${max_len}

验证PPPOE接入MTU最大输入长度
    [Arguments]    ${mtu}    ${max_len}
    转到PPPoE接入设置页面
    验证输入框最大输入长度    id=pppoe_mtu    ${mtu}    ${max_len}

验证静态接入MTU最大输入长度
    [Arguments]    ${mtu}    ${max_len}
    转到静态接入设置页面
    验证输入框最大输入长度    id=static_mtu    ${mtu}    ${max_len}

检查静态接入MTU的值是${v}
    Sleep    15
    转到静态接入设置页面
    ${a}    Get Value    id=static_mtu
    Should Be Equal    ${a}    ${v}

检查DHCP接入MTU的值是${v}
    Sleep    15
    转到DHCP接入设置页面
    ${a}    Get Value    id=dhcp_mtu
    Should Be Equal    ${a}    ${v}

检查PPPOE接入MTU的值是${v}
    Sleep    15
    转到PPPoE接入设置页面
    ${a}    Get Value    id=pppoe_mtu
    Should Be Equal    ${a}    ${v}

验证L2TP接入MTU
    [Arguments]    ${mtu}    ${message}
    转到L2TP设置页面
    设置L2TP参数    hany    969090    aaaa    mtu=${mtu}
    显示消息    ${message}
    ${rmessage}    process_special_charators    ${message}
    Run Keyword If    '${rmessage}'=='${common_unlock_save}'    检查L2TP接入MTU的值是${mtu}

验证L2TP接入MTU最大输入长度
    [Arguments]    ${mtu}    ${max_len}
    转到L2TP设置页面
    验证输入框最大输入长度    id=l2tp_mtu    ${mtu}    ${max_len}

检查L2TP接入MTU的值是${v}
    Sleep    15
    转到L2TP设置页面
    ${a}    Get Value    id=l2tp_mtu
    Should Be Equal    ${a}    ${v}

验证PPTP接入MTU
    [Arguments]    ${mtu}    ${message}
    转到PPTP设置页面
    设置PPTP参数    hany    969090    aaaa    mtu=${mtu}
    显示消息    ${message}
    ${rmessage}    process_special_charators    ${message}
    Run Keyword If    '${rmessage}'=='${common_unlock_save}'    检查PPTP接入MTU的值是${mtu}

检查PPTP接入MTU的值是${v}
    Sleep    15
    转到PPTP设置页面
    ${a}    Get Value    id=pptp_mtu
    Should Be Equal    ${a}    ${v}

验证PPTP接入MTU最大输入长度
    [Arguments]    ${mtu}    ${max_len}
    转到PPTP设置页面
    验证输入框最大输入长度    id=pptp_mtu    ${mtu}    ${max_len}
