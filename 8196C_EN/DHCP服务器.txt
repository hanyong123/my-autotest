*** Settings ***
Suite Setup       打开浏览器
Suite Teardown    关闭浏览器
Resource          keyword.txt

*** Variables ***
@{ip}             192.168.1.66    192.168.1.67    192.168.1.68    192.168.1.69    192.168.1.70    192.168.1.71    192.168.1.72
...               192.168.1.73    192.168.1.10    192.168.1.11    192.168.1.12    192.168.1.13    192.168.1.14    192.168.1.15
...               192.168.1.16    192.168.1.17    192.168.1.18    192.168.1.19    192.168.1.20    192.168.1.21
@{mac}            00:1c:c0:a2:c3:00    00:1c:c0:a2:c3:01    00:1c:c0:a2:c3:02    00:1c:c0:a2:c3:03    00:1c:c0:a2:c3:04    00:1c:c0:a2:c3:05    00:1c:c0:a2:c3:23
...               00:1c:c0:a2:c3:06    00:1c:c0:a2:c3:07    00:1c:c0:a2:c3:08    00:1c:c0:a2:c3:09    00:1c:c0:a2:c3:0a    00:1c:c0:a2:c3:0b    00:1c:c0:a2:c3:0c
...               00:1c:c0:a2:c3:0d    00:1c:c0:a2:c3:0e    00:1c:c0:a2:c3:0f    00:1c:c0:a2:c3:10    00:1c:c0:a2:c3:11    00:1c:c0:a2:c3:12

*** Test Cases ***
DhcpServer_StartIp_test_1
    [Documentation]    起始IP地址输入框超过15个字符后，无法输入
    验证DHCP开始IP最大输入长度    192.168.111.11111111111    15

DhcpServer_StartIp_test_2
    [Documentation]    起始IP地址输入框：小数点>3，提示IP长度不正确
    验证DHCP服务器    192.168.2.1.3    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_3
    [Documentation]    起始IP地址输入框：小数点<3，提示IP长度不正确
    验证DHCP服务器    192.168.2    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_4
    [Documentation]    起始IP地址不能为空
    验证DHCP服务器    ${EMPTY}    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_5
    [Documentation]    起始IP地址输入框第一段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    -1.168.1.2    192.168.1.254    ${common_ip_addr_err}
    256.168.1.2    192.168.1.254    ${common_ip_addr_err}
    0.168.1.2    192.168.1.254    ${common_ip_addr_err}
    255.168.1.2    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_6
    [Documentation]    起始IP地址输入框第二段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    192.-1.1.2    192.168.1.254    ${common_ip_addr_err}
    192.256.1.2    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_7
    [Documentation]    起始IP地址输入框第三段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    192.168.-1.2    192.168.1.254    ${common_ip_addr_err}
    192.168.256.2    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_8
    [Documentation]    起始IP地址输入框第四段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    192.168.1.-1    192.168.1.254    ${common_ip_addr_err}
    192.168.1.256    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_9
    [Documentation]    起始IP地址输入框存在非法字符，提示发现n个非法字符
    [Template]    验证DHCP服务器
    19*.168.1.8    192.168.1.254    ${common_ip_addr_err}
    192.16#.1.7    192.168.1.254    ${common_ip_addr_err}
    192.168.1'.5    192.168.1.254    ${common_ip_addr_err}
    192.168.1.9"    192.168.1.254    ${common_ip_addr_err}
    192.168.1.5\\    192.168.1.254    ${common_ip_addr_err}
    192.168.1.5/    192.168.1.254    ${common_ip_addr_err}
    192.168.1.3?    192.168.1.254    ${common_ip_addr_err}
    192.168.1’.12    192.168.1.254    ${common_ip_addr_err}
    192.168.1.23”    192.168.1.254    ${common_ip_addr_err}
    192.168.1.11、    192.168.1.254    ${common_ip_addr_err}
    192.168.1.14：    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_10
    [Documentation]    起始IP地址第一段、第二段、第三段、第四段均不能为空
    [Template]    验证DHCP服务器
    .168.1.2    192.168.1.254    ${common_ip_addr_err}
    192. 168.1.2    192.168.1.254    ${common_ip_addr_err}
    192.168. .2    192.168.1.254    ${common_ip_addr_err}
    192.168.1.    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_11
    [Documentation]    起始IP地址输入0.0.0.0，提示IP不能为全0
    验证DHCP服务器    0.0.0.0    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_12
    [Documentation]    起始IP地址不为组播地址：223 < n1 < 240
    [Template]    验证DHCP服务器
    224.168.1.4    192.168.1.254    ${common_ip_addr_err}
    225.168.1.4    192.168.1.254    ${common_ip_addr_err}
    227.168.1.4    192.168.1.254    ${common_ip_addr_err}
    230.168.1.4    192.168.1.254    ${common_ip_addr_err}
    239.168.1.4    192.168.1.254    ${common_ip_addr_err}
    240.168.1.4    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_13
    [Documentation]    起始IP地址不为回环地址：n1 = 127
    验证DHCP服务器    127.168.1.2    192.168.1.254    ${common_ip_addr_err}

DhcpServer_StartIp_test_14
    [Documentation]    起始IP地址不为保留地址：n1=0
    验证DHCP服务器    0.168.1.2    192.168.1.254    ${common_ip_addr_err}

DhcpServer_EndIp_test_15
    [Documentation]    结束IP地址输入框超过15个字符后，无法输入
    验证DHCP结束IP最大输入长度    192.168.111.444444444466666666666666666666666    15

DhcpServer_EndIp_test_16
    [Documentation]    结束IP地址输入框：小数点>3，提示IP长度不正确
    验证DHCP服务器    192.168.1.2    192.168.1.2.34    ${common_ip_addr_err}

DhcpServer_EndIp_test_17
    [Documentation]    结束IP地址输入框：小数点<3，提示IP长度不正确
    验证DHCP服务器    192.168.1.2    192.168.1    ${common_ip_addr_err}

DhcpServer_EndIp_test_18
    [Documentation]    结束IP地址不能为空
    验证DHCP服务器    192.168.1.2    ${EMPTY}    ${common_ip_addr_err}

DhcpServer_EndIp_test_19
    [Documentation]    结束IP地址输入框第一段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    192.168.1.2    -1.168.1.254    ${common_ip_addr_err}
    192.168.1.2    256.168.1.254    ${common_ip_addr_err}
    192.168.1.2    0.168.1.254    ${common_ip_addr_err}
    192.168.1.2    255.168.1.254    ${common_ip_addr_err}

DhcpServer_EndIp_test_20
    [Documentation]    结束IP地址输入框第二段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    192.168.1.2    192.-1.1.254    ${common_ip_addr_err}
    192.168.1.2    192.256.1.254    ${common_ip_addr_err}

DhcpServer_EndIp_test_21
    [Documentation]    结束IP地址输入框第三段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    192.168.1.2    192.168.-1.254    ${common_ip_addr_err}
    192.168.1.2    192.168.256.254    ${common_ip_addr_err}

DhcpServer_EndIp_test_22
    [Documentation]    结束IP地址输入框第四段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP服务器
    192.168.1.2    192.168.1.-1    ${common_ip_addr_err}
    192.168.1.2    192.168.1.256    ${common_ip_addr_err}

DhcpServer_EndIp_test_23
    [Documentation]    结束IP地址输入框存在非法字符，提示发现n个非法字符
    [Template]    验证DHCP服务器
    192.168.1.2    19*.168.1.8    ${common_ip_addr_err}
    192.168.1.2    192.16#.1.7    ${common_ip_addr_err}
    192.168.1.2    192.168.1'.5    ${common_ip_addr_err}
    192.168.1.2    192.168.1.9"    ${common_ip_addr_err}
    192.168.1.2    192.168.1.5\\    ${common_ip_addr_err}
    192.168.1.2    192.168.1.5/    ${common_ip_addr_err}
    192.168.1.2    192.168.1.3?    ${common_ip_addr_err}
    192.168.1.2    192.168.1’.12    ${common_ip_addr_err}
    192.168.1.2    192.168.1.23”    ${common_ip_addr_err}
    192.168.1.2    192.168.1.11、    ${common_ip_addr_err}
    192.168.1.2    192.168.1.14：    ${common_ip_addr_err}

DhcpServer_EndIp_test_24
    [Documentation]    结束IP地址第一段、第二段、第三段、第四段均不能为空
    [Template]    验证DHCP服务器
    192.168.1.2    .168.1.2    ${common_ip_addr_err}
    192.168.1.2    192. 168.1.2    ${common_ip_addr_err}
    192.168.1.2    192.168. .2    ${common_ip_addr_err}
    192.168.1.2    192.168.1.    ${common_ip_addr_err}

DhcpServer_EndIp_test_25
    [Documentation]    结束IP地址输入0.0.0.0，提示IP不能为全0
    验证DHCP服务器    192.168.1.2    0.0.0.0    ${common_ip_addr_err}

DhcpServer_EndIp_test_26
    [Documentation]    结束IP地址不为组播地址：223 < n1 < 240
    [Template]    验证DHCP服务器
    192.168.1.2    224.168.1.4    ${common_ip_addr_err}
    192.168.1.2    225.168.1.4    ${common_ip_addr_err}
    192.168.1.2    227.168.1.4    ${common_ip_addr_err}
    192.168.1.2    230.168.1.4    ${common_ip_addr_err}
    192.168.1.2    239.168.1.4    ${common_ip_addr_err}
    192.168.1.2    240.168.1.4    ${common_ip_addr_err}

DhcpServer_EndIp_test_27
    [Documentation]    结束IP地址不为回环地址：n1 = 127
    验证DHCP服务器    192.168.1.2    127.168.1.254    ${common_ip_addr_err}

DhcpServer_EndIp_test_28
    [Documentation]    结束IP地址不为保留地址：n1=0
    验证DHCP服务器    192.168.1.2    0.168.1.254    ${common_ip_addr_err}

DhcpServer_PoolRange_test_29
    [Documentation]    设置DHCP服务器地址池范围超过地址池IP个数最大值63，提示超出最大范围
    ...
    ...    注：该用例需要本机IP设置为自动获取
    [Setup]    Run Keyword    set_host_if_dhcp    ${host_lan_if_name}
    [Template]
    验证DHCP服务器    192.168.1.1    192.168.1.254    ${common_lan_addr_err}
    验证DHCP服务器    192.168.1.0    192.168.1.254    ${common_segment_err}
    验证DHCP服务器    192.168.1.2    192.168.1.255    ${common_Bcast_err}
    验证DHCP服务器    192.168.0.2    192.168.1.254    ${common_lan_ip_err}
    修改LAN口IP    192.168.0.1    255.255.0.0
    转到内网配置页面
    验证DHCP服务器    192.168.0.2    192.168.1.2    ${common_addr_err}
    修改LAN口IP    192.168.1.1    255.255.255.0
    [Teardown]    Run Keyword    set_host_if_static    ${host_lan_if_name}    ${host_lan_ip}    ${host_lan_mask}    ${router_lan_ip}

DhcpServer_PoolRange_test_30
    [Documentation]    设置DHCP服务器地址池起始IP小于或等于结束IP，保存成功，地址池的范围从“起始IP”到“结束IP”
    [Template]
    验证DHCP服务器    192.168.1.4    192.168.1.54    ${common_unlock_save}
    检查DHCP服务器是否保存成功    192.168.1.4    192.168.1.54
    验证DHCP服务器    192.168.1.4    192.168.1.4    ${common_unlock_save}
    检查DHCP服务器是否保存成功    192.168.1.4    192.168.1.4
    验证DHCP服务器    192.168.1.2    192.168.1.254    ${common_unlock_save}
    检查DHCP服务器是否保存成功    192.168.1.2    192.168.1.254

DhcpServer_PoolRange_test_31
    [Documentation]    设置DHCP服务器地址池起始IP大于结束IP，保存失败，提示“地址池起始IP地址不能大于结束IP地址”
    [Template]    验证DHCP服务器
    192.168.1.45    192.168.1.2    ${common_cmp_ip_err}
    192.168.1.254    192.168.1.253    ${common_cmp_ip_err}
    192.168.1.33    192.168.1.3    ${common_cmp_ip_err}

DhcpServer_ReserveIp_IP_test_123
    [Documentation]    IP地址输入框超过15个字符后，无法输入
    验证保留地址IP最大输入长度    192.168.1.222222222222222222222222    15

DhcpServer_ReserveIp_IP_test_124
    [Documentation]    IP地址输入框：小数点>3，提示IP长度不正确
    验证DHCP保留地址设置    ${EMPTY}    192.168.1.4.3    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_125
    [Documentation]    IP地址输入框：小数点<3，提示IP长度不正确
    验证DHCP保留地址设置    ${EMPTY}    192.168.1    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_126
    [Documentation]    IP地址不能为空
    验证DHCP保留地址设置    ${EMPTY}    ${EMPTY}    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_127
    [Documentation]    IP地址输入框第一段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    -1.168.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    256.168.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    0.168.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    255.168.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_128
    [Documentation]    IP地址输入框第二段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.-1.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.256.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_129
    [Documentation]    IP地址输入框第三段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.-1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.256.2    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_130
    [Documentation]    IP地址输入框第四段IP值超出范围，提示只能在0~255之间
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.-1    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.256    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_131
    [Documentation]    IP地址输入框存在非法字符，提示发现n个非法字符
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    19*.168.1.8    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.16#.1.7    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1'.5    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.9"    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.5\\    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.5/    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.3?    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1’.12    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.23”    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.11、    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.14：    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_132
    [Documentation]    IP地址第一段、第二段、第三段、第四段均不能为空
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    .168.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192. 168.1.2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168. .2    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    192.168.1.    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_133
    [Documentation]    IP地址输入0.0.0.0，提示IP不能为全0
    验证DHCP保留地址设置    ${EMPTY}    0.0.0.0    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_134
    [Documentation]    IP地址不为组播地址：223 < n1 < 240
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    224.168.1.4    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    225.168.1.4    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    227.168.1.4    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    230.168.1.4    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    239.168.1.4    08:10:76:22:b8:8d    ${common_ip_addr_err}
    ${EMPTY}    240.168.1.4    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_135
    [Documentation]    IP地址不为回环地址：n1 = 127
    验证DHCP保留地址设置    ${EMPTY}    127.168.1.5    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_IP_test_136
    [Documentation]    IP地址不为保留地址：n1=0
    验证DHCP保留地址设置    ${EMPTY}    0.168.1.5    08:10:76:22:b8:8d    ${common_ip_addr_err}

DhcpServer_ReserveIp_MAC_test_137
    [Documentation]    MAC配置输入框不能为空
    验证DHCP保留地址设置    ${EMPTY}    192.168.1.3    ${EMPTY}    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_138
    [Documentation]    MAC配置输入框超出17个字符后无法输入
    验证保留地址MAC最大输入长度    08:10:76:22:b8:8d5555555555555    17

DhcpServer_ReserveIp_MAC_test_139
    [Documentation]    MAC配置输入框长度不满17个字符，提示格式错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    08:10:76:22:b8:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    08:10:7:22:b8:8d    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    0:10:76:22:b8:8d    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_140
    [Documentation]    MAC地址的分隔符不全为-时，提示格式错误
    验证DHCP保留地址设置    ${EMPTY}    192.168.1.3    08-10-76-24-78:7    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_141
    [Documentation]    MAC地址的分隔符不全为:时，提示格式错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    08-10:76:22:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    08:10-76:22:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    08:10:76-22:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    08:10:76:22-b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    08:10:76:22:b8-87    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_142
    [Documentation]    MAC地址的分隔符个数不等于5，提示错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    08:10:76:22:b8764    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    08-10-76-22-b8764    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    01234567890123456    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    010101-0123402857    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    010101:0123402857    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_143
    [Documentation]    MAC地址的内容超出00~FF的范围时，提示格式错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    fg:10:76:22:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    gf:10:76:22:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:fg:76:22:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:10:fg:22:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:10:76:fg:b8:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:10:76:22:fg:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:10:76:22:b8:fg    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_144
    [Documentation]    MAC地址中的内容有不属于｛0-9，A-F，a-f｝范围的，提示格式错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    0*:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1(-c0-a2-c3-87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:00:^0:a:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-00-a#-c3-95    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:ac:c0:00:c':87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-c0-a2-c3-8"    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-c0-a2-c3-8/    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:ac:c0:00:c’:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-c0-a2-c3-8”    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-c0-a2-c3-8。    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-c0-a2-c3-8、    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_145
    [Documentation]    MAC配置输入框以xx-xx-xx-xx-xx-xx格式输入MAC地址，xx不能为空
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    :1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00- -c0-a2-c3-87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:00: :a:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-00- -c3-95    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:ac:c0:00: :87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:ac:c0:00:c3:    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_146
    [Documentation]    MAC配置输入框以xx-xx-xx-xx-xx-xx格式输入MAC地址，xx不能大于2字节
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    123:1c:c0:a2:c3:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-123-c0-a2-c3-8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:00:134 :a:c3:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00-1c-00-345-c3-9    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:ac:c0:00:533:8    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:ac:c0:00:c:657    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_147
    [Documentation]    MAC地址为全0时 00-00-00-00-00-00，提示格式错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    00-00-00-00-00-00    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    00:00:00:00:00:00    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_148
    [Documentation]    MAC地址为全F时 FF-FF-FF-FF-FF-FF，提示格式错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    ff:ff:ff:ff:ff:ff    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    FF-FF-FF-FF-FF-FF    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    FF:FF:FF:FF:FF:FF    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    ff-ff-ff-ff-ff-ff    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_149
    [Documentation]    MAC地址为组播地址时 xn-xx-xx-xx-xx-xx（n属于[1,3,5,7,9,B,D,F]），提示格式错误
    [Template]    验证DHCP保留地址设置
    ${EMPTY}    192.168.1.3    31:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    33:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    35:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    37:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    39:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    3B:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    3D:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    3F:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    3b:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    3f:1c:c0:a2:c3:87    ${common_mac_addr_err}
    ${EMPTY}    192.168.1.3    3d:1c:c0:a2:c3:87    ${common_mac_addr_err}

DhcpServer_ReserveIp_MAC_test_150
    [Documentation]    MAC地址符合格式规范，成功保存并生效
    删除DHCP保留地址列表所有行
    Sleep    3
    Set Selenium Speed    0.5
    验证DHCP保留地址设置    ${EMPTY}    192.168.1.99    00:1c:c0:a2:c3:dd    ${common_unlock_add}
    验证DHCP保留地址设置    ${EMPTY}    192.168.1.100    00:1c:c0:a2:c3:ee    ${common_unlock_add}
    DHCP保留地址列表含有192.168.1.99
    DHCP保留地址列表含有00:1c:c0:a2:c3:dd
    DHCP保留地址列表含有192.168.1.100
    DHCP保留地址列表含有00:1c:c0:a2:c3:ee
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Name_test_151
    [Documentation]    规则名为空时，不能添加条目，提示"规则名不能为空"
    Set Selenium Speed    0.5
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    ${EMPTY}    192.168.1.99    00:1c:c0:a2:c3:dd    ${common_unlock_add}
    DHCP保留地址列表含有${EMPTY}
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Name_test_152
    [Documentation]    规则名含特殊字符，提示不能含特殊字符“不能含有非法字符,.:;'"~`@#$%^&*(){}[]<>?/\和空格”
    [Template]    验证DHCP保留地址设置
    34'    192.168.1.3    31:1c:c0:a2:c3:87    ${common_string_err}
    34"    192.168.1.3    33:1c:c0:a2:c3:87    ${common_string_err}
    34\\    192.168.1.3    35:1c:c0:a2:c3:87    ${common_string_err}
    3 4    192.168.1.3    37:1c:c0:a2:c3:87    ${common_string_err}

DhcpServer_ReserveIp_Name_test_153
    [Documentation]    规则名长度最大31个字符，超过31字符无法输入
    验证保留地址规则名最大输入长度    0123456789012345678901234567890123456789    31

DhcpServer_ReserveIp_Name_test_154
    [Documentation]    规则名输入30个字符，再输入一个中文，导致超过31个字符，提示“已对规则名自动截短”。
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    012345678901234567890123456789我    192.168.1.33    00:1c:c0:a2:c4:dd    ${common_unlock_add}
    DHCP保留地址列表第1行第2列是012345678901234567890123456789
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Add_test_155
    [Documentation]    输入正确的IP地址、MAC地址、规则名，点击“增加”按钮；提示“操作成功！”，并刷新列表
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    11    192.168.1.44    00:1c:c0:a2:c4:aa    ${common_unlock_add}
    验证DHCP保留地址设置    45    192.168.1.67    00:1c:c0:a2:c4:80    ${common_unlock_add}
    DHCP保留地址列表某一行内容    1    45    192.168.1.67    00:1c:c0:a2:c4:80
    DHCP保留地址列表某一行内容    2    11    192.168.1.44    00:1c:c0:a2:c4:aa
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Add_test_156
    [Documentation]    当已添加512条条目时，继续添加第513条条目，提示“添加失败，已达到最大条目数！”
    删除DHCP保留地址列表所有行
    Sleep    5
    : FOR    ${i}    IN RANGE    20
    \    验证DHCP保留地址设置    ${EMPTY}    @{ip}[${i}]    @{mac}[${i}]    ${common_unlock_add}
    验证DHCP保留地址设置    ${EMPTY}    192.168.1.22    00:1c:c0:a2:c3:13    ${common_item_err}
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Add_test_157
    [Documentation]    添加条目，IP地址和已有条目的地址重复，提示已有相同DHCP保留地址条目
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    65    192.168.1.5    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证DHCP保留地址设置    87    192.168.1.5    36:1c:c0:a2:c3:aa    ${common_ip_occupy_err}
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Add_test_158
    [Documentation]    添加条目，MAC地址和已有条目的地址重复，提示已有相同DHCP保留地址条目
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    77    192.168.1.8    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证DHCP保留地址设置    88    192.168.1.11    36:1c:c0:a2:c3:88    ${common_unlock_add}
    DHCP保留地址列表某一行内容    1    88    192.168.1.11    36:1c:c0:a2:c3:88
    DHCP保留地址列表某一行内容    2    77    192.168.1.8    36:1c:c0:a2:c3:88
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Modify_test_159
    [Documentation]    成功修改一条DHCP保留地址条目
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    65    192.168.1.6    36:1c:c0:a2:c3:88    ${common_unlock_add}
    修改DHCP保留地址列表第1行的规则名为jjjIP地址为192.168.1.55MAC地址为36:1c:c0:a2:c3:80并显示消息${common_unlock_add}
    DHCP保留地址列表某一行内容    1    jjj    192.168.1.55    36:1c:c0:a2:c3:80
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Modify_test_160
    [Documentation]    修改条目，IP地址和已有条目的地址重复，提示已有相同DHCP保留地址条目
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    1    192.168.1.99    36:1c:c0:a2:c3:82    ${common_unlock_add}
    验证DHCP保留地址设置    2    192.168.1.88    36:1c:c0:a2:c3:83    ${common_unlock_add}
    修改DHCP保留地址列表第1行的规则名为jjjIP地址为192.168.1.99MAC地址为36:1c:c0:a2:c3:86并显示消息${common_ip_occupy_err}
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Modify_test_161
    [Documentation]    修改条目，MAC地址和已有条目的地址重复，提示已有相同DHCP保留地址条目
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    aa    192.168.1.99    36:1c:c0:a2:c3:82    ${common_unlock_add}
    验证DHCP保留地址设置    bb    192.168.1.88    36:1c:c0:a2:c3:83    ${common_unlock_add}
    修改DHCP保留地址列表第1行的规则名为bbIP地址为192.168.1.88MAC地址为36:1c:c0:a2:c3:82并显示消息${common_unlock_add}
    DHCP保留地址列表某一行内容    1    bb    192.168.1.88    36:1c:c0:a2:c3:82
    DHCP保留地址列表某一行内容    2    aa    192.168.1.99    36:1c:c0:a2:c3:82
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Delete_test_163
    [Documentation]    点击条目对应的删除按钮，成功删除一条DHCP保留地址条目，提示“操作成功！”，并刷新列表
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    aa    192.168.1.3    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证DHCP保留地址设置    bb    192.168.1.4    36:1c:c0:a2:c3:aa    ${common_unlock_add}
    删除DHCP保留地址列表第1行并显示成功消息
    验证DHCP保留地址某一行是否删除成功    1    bb    192.168.1.4    36:1c:c0:a2:c3:aa
    删除DHCP保留地址列表所有行

DhcpServer_ReserveIp_Delete_test_164
    [Documentation]    点击删除全部按钮，成功删除所有DHCP保留地址条目，提示“操作成功！”，并刷新列表
    删除DHCP保留地址列表所有行
    Sleep    3
    验证DHCP保留地址设置    aa    192.168.1.3    36:1c:c0:a2:c3:88    ${common_unlock_add}
    验证DHCP保留地址设置    bb    192.168.1.4    36:1c:c0:a2:c3:85    ${common_unlock_add}
    验证DHCP保留地址设置    cc    192.168.1.5    36:1c:c0:a2:c3:86    ${common_unlock_add}
    删除DHCP保留地址列表所有行
    DHCP保留地址列表没有aa
    DHCP保留地址列表没有192.168.1.3
    DHCP保留地址列表没有36:1c:c0:a2:c3:88
    DHCP保留地址列表没有bb
    DHCP保留地址列表没有192.168.1.4
    DHCP保留地址列表没有36:1c:c0:a2:c3:85
    DHCP保留地址列表没有cc
    DHCP保留地址列表没有192.168.1.5
    DHCP保留地址列表没有36:1c:c0:a2:c3:86

DhcpServer_ReserveIp_FastReserve_test_165
    [Documentation]    对列表中的一条类型为“动态”的DHCP地址，点击“保留地址”按钮，提示“操作成功！”，该DHCP地址的类型改变为“静态”
    Set Selenium Speed    0.5
    转到内网配置页面
    Sleep    3
    ${len}    计算客户端列表总共多少行
    Run Keyword If    '${len}'>='1'    修改客户端列表中某行Reserved状态    1
    Run Keyword If    '${len}'>='1'    验证客户端列表中某行是否存在于保留地址列表    1    1
    Run Keyword If    '${len}'>='1'    检查客户端列表中某行Reserved的状态    1    1

DhcpServer_ReserveIp_FastReserve_test_166
    [Documentation]    点击“保留全部”按钮，原类型为“动态”的所有DHCP地址，类型全部改变为“静态”
    Set Selenium Speed    0.5
    转到内网配置页面
    Sleep    3
    ${len}    计算客户端列表总共多少行
    Run Keyword If    '${len}'>='1'    Click Element    id=dhcp_keep_all
    : FOR    ${i}    IN RANGE    ${len}
    \    验证客户端列表中某行是否存在于保留地址列表    ${i}+1    1
    \    检查客户端列表中某行Reserved的状态    ${i}+1    1

*** Keywords ***
验证DHCP服务器
    [Arguments]    ${startIp}    ${endIp}    ${message}
    转到内网配置页面
    Sleep    3
    设置DHCP服务器    ${startIp}    ${endIp}
    显示消息    ${message}

验证DHCP开始IP最大输入长度
    [Arguments]    ${startIp}    ${max_len}
    转到内网配置页面
    验证输入框最大输入长度    id=dhcp_start_ip    ${startIp}    ${max_len}

验证DHCP结束IP最大输入长度
    [Arguments]    ${startIp}    ${max_len}
    转到内网配置页面
    验证输入框最大输入长度    id=dhcp_end_ip    ${startIp}    ${max_len}

DHCP保留地址列表含有${string}
    Table Should Contain    xpath=//table[@class="df_tab"]    ${string}

DHCP保留地址列表第${row}行第${col}列是${string}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    ${col}
    Should Be Equal    ${v}    ${string}

删除DHCP保留地址列表第${row}行并显示成功消息
    Click Element    xpath=//div[@class="tab_del_btn"][${row}]
    ${message}    Confirm Action
    Should Be Equal    ${message}    ${common_del_one_tip}
    显示消息    ${common_unlock_delete}

修改DHCP保留地址列表第${row}行的规则名为${name}IP地址为${ip}MAC地址为${mac}并显示消息${message}
    Click Element    xpath=(//div[@class="tab_mod_btn"])[${row}]
    Sleep    2
    Input Text    id=reserve_des    ${name}
    Input Text    id=reserve_ip    ${ip}
    Input Text    id=reserve_mac    ${mac}
    Click Button    id=add_reservation
    显示消息    ${message}

删除DHCP保留地址列表所有行
    转到DHCP保留地址设置页面
    Click Element    id=del_all_reservation
    ${message}    Confirm Action
    Should Be Equal    ${message}    ${common_del_tip}
    显示消息    ${common_unlock_delete}

DHCP保留地址列表没有${string}
    Run Keyword And Expect Error    *    Table Should Contain    xpath=//table[@class="df_tab"]    ${string}

修改LAN口IP
    [Arguments]    ${ip}    ${mask}
    Choose Ok On Next Confirmation
    转到内网配置页面
    设置LAN口IP和MASK    ${ip}    ${mask}
    ${message}    Confirm Action
    Should Be Equal    ${message}    ${common_reboot_tip}
    Sleep    40s
    关闭浏览器
    Open Browser    http://${login_user}:${login_passwd}@${ip}/index.htm    ${browser}
    Sleep    5

检查DHCP服务器是否保存成功
    [Arguments]    ${startIp}    ${endIp}
    转到内网配置页面
    Sleep    3
    ${v}    Get Value    id=dhcp_start_ip
    Should Be Equal    ${v}    ${startIp}
    ${v}    Get Value    id=dhcp_end_ip
    Should Be Equal    ${v}    ${endIp}

验证保留地址IP最大输入长度
    [Arguments]    ${ip}    ${max_len}
    转到DHCP保留地址设置页面
    验证输入框最大输入长度    id=reserve_ip    ${ip}    ${max_len}

验证保留地址MAC最大输入长度
    [Arguments]    ${mac}    ${max_len}
    转到DHCP保留地址设置页面
    验证输入框最大输入长度    id=reserve_mac    ${mac}    ${max_len}

验证保留地址规则名最大输入长度
    [Arguments]    ${des}    ${max_len}
    转到DHCP保留地址设置页面
    验证输入框最大输入长度    id=reserve_des    ${des}    ${max_len}

验证DHCP保留地址设置
    [Arguments]    ${des}    ${ip}    ${mac}    ${message}
    转到DHCP保留地址设置页面
    设置DHCP服务器保留地址    ${des}    ${ip}    ${mac}
    显示消息    ${message}
    ${rmessage}    process_special_charators    ${message}
    Run Keyword If    '${rmessage}'=='${common_unlock_add}'    Sleep    10

修改客户端列表中某行Reserved状态
    [Arguments]    ${row}    ${isReserve}=0
    转到内网配置页面
    ${row}    Evaluate    int(${row})+1
    Run Keyword If    '{isReserve}'=='0'    Click Element    xpath=//div[@class="tab_link_btn"][${row}]
    Run Keyword If    '{isReserve}'=='1'    Click Element    xpath=//div[@class="tab_can_btn"][${row}]
    Run Keyword If    '{isReserve}'=='0'    显示消息    ${common_unlock_add}
    Run Keyword If    '{isReserve}'=='1'    显示消息    ${common_unlock_save}

验证客户端列表中某行是否存在于保留地址列表
    [Arguments]    ${row}    ${isReserve}=0
    Sleep    3
    ${row}    Evaluate    int(${row})+1
    ${ip}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    2
    ${mac}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    3
    转到DHCP保留地址设置页面
    Sleep    3
    Run Keyword If    '{isReserve}'=='0'    DHCP保留地址列表没有${ip}
    Run Keyword If    '{isReserve}'=='1'    DHCP保留地址列表含有${ip}
    Run Keyword If    '{isReserve}'=='1'    DHCP保留地址列表含有${mac}

检查客户端列表中某行Reserved的状态
    [Arguments]    ${row}    ${isReserve}=0
    转到内网配置页面
    Sleep    3
    ${row}    Evaluate    int(${row})+1
    ${status}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    5
    Run Keyword If    '{isReserve}'=='0'    Should Be Equal    ${status}    No
    Run Keyword If    '{isReserve}'=='1'    Should Be Equal    ${status}    Yes

DHCP保留地址列表某一行内容
    [Arguments]    ${row}    ${des}    ${ip}    ${mac}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    2
    Should Be Equal    ${v}    ${des}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    3
    Should Be Equal    ${v}    ${ip}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    4
    Should Be Equal    ${v}    ${mac}

验证DHCP保留地址某一行是否删除成功
    [Arguments]    ${row}    ${des}    ${ip}    ${mac}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    2
    Should Not Be Equal    ${v}    ${des}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    3
    Should Not Be Equal    ${v}    ${ip}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    4
    Should Not Be Equal    ${v}    ${mac}

计算客户端列表总共多少行
    ${len}    Set Variable    0
    : FOR    ${i}    IN RANGE    2    32
    \    ${status}    Run Keyword And Return Status    Get Table Cell    xpath=//table[@class="df_tab"]    ${i}    1
    \    Run Keyword If    '${status}'=='False'    Exit For Loop
    \    ${len}    Evaluate    int(${i})-1
    [Return]    ${len}
