*** Settings ***
Suite Setup       Run Keywords    打开浏览器    AND    转到SUPER-DMZ设置页面
Suite Teardown    关闭浏览器
Test Setup        等待时间
Resource          keyword.txt

*** Test Cases ***
SuperDMZ_MAC_test_1
    [Documentation]    MAC地址符合格式规范，成功设置SUPER DMZ主机MAC地址
    [Template]
    验证MAC地址    12-00-11-22-33-44    ${common_unlock_save}
    验证保存成功的结果    12:00:11:22:33:44
    验证MAC地址    AA-BB-CD-CD-EE-FF    ${common_unlock_save}
    验证保存成功的结果    aa:bb:cd:cd:ee:ff

SuperDMZ_MAC_test_2
    [Documentation]    MAC配置输入框不能为空
    [Template]    验证MAC地址
    ${EMPTY}    ${common_mac_addr_err}

SuperDMZ_MAC_test_3
    [Documentation]    MAC配置输入框超出17个字符后无法输入
    [Template]
    Click Element    super_dmz_enable_on
    验证MAC最大输入长度    123456789012345678    17

SuperDMZ_MAC_test_4
    [Documentation]    MAC配置输入框长度不满17个字符，提示格式错误
    [Template]    验证MAC地址
    02-00-00-00-00-0    ${common_mac_addr_err}

SuperDMZ_MAC_test_5
    [Documentation]    MAC地址的分隔符不全为-时，提示格式错误
    [Template]    验证MAC地址
    06-02-08-08-08:08    ${common_mac_addr_err}
    06-02-08-08:08-08    ${common_mac_addr_err}
    06-02-08:08-08-08    ${common_mac_addr_err}
    06-02:08-08-08-08    ${common_mac_addr_err}
    06:02-08-08-08-08    ${common_mac_addr_err}

SuperDMZ_MAC_test_6
    [Documentation]    MAC地址的分隔符不全为:时，提示格式错误
    [Template]    验证MAC地址
    06:02:08:08:08-08    ${common_mac_addr_err}
    06:02:08:08-08:08    ${common_mac_addr_err}
    06:02:08-08:08:08    ${common_mac_addr_err}
    06:02-08:08:08:08    ${common_mac_addr_err}
    06-02:08:08:08:08    ${common_mac_addr_err}

SuperDMZ_MAC_test_7
    [Documentation]    MAC地址的分隔符个数不等于5，提示错误
    [Template]    验证MAC地址
    06-02-08-08-08    ${common_mac_addr_err}
    06-02-08-08--08-    ${common_mac_addr_err}

SuperDMZ_MAC_test_8
    [Documentation]    MAC地址的内容超出00~FF的范围时，提示格式错误
    [Template]    验证MAC地址
    G0-02-08-08-08-08    ${common_mac_addr_err}
    GG-02-08-08-08-08    ${common_mac_addr_err}
    gg-02-08-08-08-08    ${common_mac_addr_err}
    0g-02-08-08-08-08    ${common_mac_addr_err}
    06-kk-08-08-08-08    ${common_mac_addr_err}
    06-02-rr-08-08-08    ${common_mac_addr_err}
    06-02-08-HH-08-08    ${common_mac_addr_err}
    06-02-08-08-PP-08    ${common_mac_addr_err}
    06-02-08-08-08-LL    ${common_mac_addr_err}

SuperDMZ_MAC_test_9
    [Documentation]    MAC地址中的内容有不属于｛0-9，A-F，a-f｝范围的，提示格式错误
    [Template]    验证MAC地址
    08-02-08-08-08-0G    ${common_mac_addr_err}
    08-02-08-08-0G-08    ${common_mac_addr_err}
    08-02-08-0G-08-08    ${common_mac_addr_err}
    08-02-0G-08-08-08    ${common_mac_addr_err}
    08-0G-08-08-08-08    ${common_mac_addr_err}
    0G-02-08-08-08-08    ${common_mac_addr_err}
    0X-0Y-0Z-08-08-08    ${common_mac_addr_err}
    0x-0y-0z-08-08-08    ${common_mac_addr_err}
    8 - 8-08-08-08-08    ${common_mac_addr_err}
    8\-\8-08-08-08-08    ${common_mac_addr_err}
    8"-"8-08-08-08-08    ${common_mac_addr_err}
    8'-'8-08-08-08-08    ${common_mac_addr_err}

SuperDMZ_MAC_test_10
    [Documentation]    MAC配置输入框以xx-xx-xx-xx-xx-xx格式输入MAC地址，xx不能为空
    [Template]    验证MAC地址
    08-02-08-08-08-    ${common_mac_addr_err}
    08-02-08-08--08    ${common_mac_addr_err}
    08-02-08--08-08    ${common_mac_addr_err}
    08-02--08-08-08    ${common_mac_addr_err}
    08--08-08-08-08    ${common_mac_addr_err}
    -08-08-08-08-08    ${common_mac_addr_err}

SuperDMZ_MAC_test_11
    [Documentation]    MAC配置输入框以xx-xx-xx-xx-xx-xx格式输入MAC地址，xx不能大于2字节
    [Template]    验证MAC地址
    08-02-08-08-08-F    ${common_mac_addr_err}
    08-02-08-08-F-08    ${common_mac_addr_err}
    08-02-08-F-08-08    ${common_mac_addr_err}
    08-02-F-08-08-08    ${common_mac_addr_err}
    08-F-08-08-08-08    ${common_mac_addr_err}
    F-08-08-08-08-08    ${common_mac_addr_err}
    08-02-08-08-0AF-08    ${common_mac_addr_err}
    08-02-08-0AF-08-08    ${common_mac_addr_err}
    08-02-0AF-08-08-08    ${common_mac_addr_err}
    08-0AF-08-08-08-08    ${common_mac_addr_err}
    0AF-08-08-08-08-08    ${common_mac_addr_err}

SuperDMZ_MAC_test_12
    [Documentation]    MAC地址为全0时 00-00-00-00-00-00，提示格式错误
    [Template]    验证MAC地址
    00-00-00-00-00-00    ${common_mac_addr_err}

SuperDMZ_MAC_test_13
    [Documentation]    MAC地址为全F时 FF-FF-FF-FF-FF-FF，提示格式错误
    [Template]    验证MAC地址
    FF-FF-FF-FF-FF-FF    ${common_mac_addr_err}
    ff-ff-ff-ff-ff-ff    ${common_mac_addr_err}

SuperDMZ_MAC_test_14
    [Documentation]    MAC地址为组播地址时 xn-xx-xx-xx-xx-xx（n属于[1,3,5,7,9,B,D,F]），提示格式错误
    [Template]    验证MAC地址
    F1-08-08-08-08-08    ${common_mac_addr_err}
    F3-08-08-08-08-08    ${common_mac_addr_err}
    F5-08-08-08-08-08    ${common_mac_addr_err}
    F7-08-08-08-08-08    ${common_mac_addr_err}
    F9-08-08-08-08-08    ${common_mac_addr_err}
    FB-08-08-08-08-08    ${common_mac_addr_err}
    FD-08-08-08-08-08    ${common_mac_addr_err}
    FF-08-08-08-08-08    ${common_mac_addr_err}
    Fb-08-08-08-08-08    ${common_mac_addr_err}
    Fd-08-08-08-08-08    ${common_mac_addr_err}
    Ff-08-08-08-08-08    ${common_mac_addr_err}

SuperDMZ_MAC_test_15
    [Documentation]    成功设置主机MAC地址
    验证MAC地址    12-00-11-22-33-44    ${common_unlock_save}
    验证保存成功的结果    12:00:11:22:33:44
    验证MAC地址    AA-BB-CD-CD-EE-FF    ${common_unlock_save}
    验证保存成功的结果    aa:bb:cd:cd:ee:ff

SuperDMZ_MAC_test_16
    [Documentation]    成功设置主机MAC地址
    验证MAC地址    12-00-11-22-33-44    ${common_unlock_save}
    Sleep    1
    验证MAC输入框为disable    0    ${common_unlock_save}

*** Keywords ***
验证MAC地址
    [Arguments]    ${wmac}    ${message}
    设置SUPER-DMZ参数    mac=${wmac}
    显示消息    ${message}

验证MAC最大输入长度
    [Arguments]    ${wmac}    ${max_len}
    验证输入框最大输入长度    id=super_dmz_addr    ${wmac}    ${max_len}

验证保存成功的结果
    [Arguments]    ${mac}
    Element Should Be Enabled    id=super_dmz_addr
    ${i}    Get Value    id=super_dmz_addr
    Should Be Equal    ${i}    ${mac}

验证MAC输入框为disable
    [Arguments]    ${wenable}    ${message}
    设置SUPER-DMZ参数    enable=${wenable}
    ${s}    set variable    ${wenable}
    显示消息    ${message}
    Element Should Be Disabled    id=super_dmz_addr

等待时间
    Sleep    5
