*** Settings ***
Suite Setup       Run Keywords    打开浏览器    AND    转到虚拟服务配置页面
Suite Teardown    关闭浏览器
Test Setup        等待时间
Resource          keyword.txt

*** Test Cases ***
VirtualServers_NAME_test_1
    [Documentation]    规则名称允许为空，为空时能够保存成功；规则列表中展示出的该条规则的规则名为空
    验证规则名    ${EMPTY}    ${common_unlock_save}
    列表第row行第col列是string    1    2    ${EMPTY}

VirtualServers_NAME_test_2
    [Documentation]    规则名称输入数据带有 '"\或空格 ，提示“ 不能含有特殊字符 \ '"\和空格 ”
    [Template]    验证规则名
    aa'aa    ${common_string_err}
    aa"aa    ${common_string_err}
    aa\\aa    ${common_string_err}
    aa aa    ${common_string_err}

VirtualServers_NAME_test_3
    [Documentation]    规则名长度最大31个字符，超过31字符无法输入
    [Template]    验证最大输入长度
    vir_name    1111111111222222222233333333334我    31

VirtualServers_NAME_test_4
    [Documentation]    规则名输入30个字符，再输入一个中文，导致超过31个字符，提示“规则名过长，已经自动截断”。
    验证规则名    111111111122222222223333333333我    ${common_unlock_save}
    列表第row行第col列是string    1    2    111111111122222222223333333333

VirtualServers_InternalIP_test_5
    [Documentation]    内网IP地址输入框超过15个字符后，无法输入
    [Template]    验证最大输入长度
    vir_ip    1234567890123456    15

VirtualServers_InternalIP_test_6
    [Documentation]    内网IP地址输入框：小数点>3，提示IP长度不正确
    [Template]    验证内网IP地址
    192.168.1.30.    ${common_ip_addr_err}

VirtualServers_InternalIP_test_7
    [Documentation]    内网IP地址输入框：小数点<3，提示IP长度不正确
    [Template]    验证内网IP地址
    192.168.130    ${common_ip_addr_err}

VirtualServers_InternalIP_test_8
    [Documentation]    内网IP地址不能为空
    [Template]    验证内网IP地址
    ${EMPTY}    ${common_ip_addr_err}

VirtualServers_InternalIP_test_9
    [Documentation]    内网IP地址输入框第一段IP值超出范围，提示只能在0~255之间
    [Template]    验证内网IP地址
    -1.168.1.30    ${common_ip_addr_err}
    256.168.1.30    ${common_ip_addr_err}

VirtualServers_InternalIP_test_10
    [Documentation]    内网IP地址输入框第二段IP值超出范围，提示只能在0~255之间
    [Template]    验证内网IP地址
    192.-1.1.30    ${common_ip_addr_err}
    192.256.1.30    ${common_ip_addr_err}

VirtualServers_InternalIP_test_11
    [Documentation]    内网IP地址输入框第三段IP值超出范围，提示只能在0~255之间
    [Template]    验证内网IP地址
    192.168.-1.30    ${common_ip_addr_err}
    192.168.256.30    ${common_ip_addr_err}

VirtualServers_InternalIP_test_12
    [Documentation]    内网IP地址输入框第四段IP值超出范围，提示只能在0~255之间
    [Template]    验证内网IP地址
    192.168.1.-1    ${common_ip_addr_err}
    192.168.1.256    ${common_ip_addr_err}

VirtualServers_InternalIP_test_13
    [Documentation]    内网IP地址输入框存在非法字符，提示发现n个非法字符
    [Template]    验证内网IP地址
    192,168;1:s    ${common_ip_addr_err}

VirtualServers_InternalIP_test_14
    [Documentation]    内网IP地址第一段、第二段、第三段、第四段均不能为空
    [Template]    验证内网IP地址
    .168.1.10    ${common_ip_addr_err}
    192..1.10    ${common_ip_addr_err}
    192.168..10    ${common_ip_addr_err}
    192.168.1.    ${common_ip_addr_err}

VirtualServers_InternalIP_test_15
    [Documentation]    内网IP地址输入0.0.0.0，提示IP不能为全0
    [Template]    验证内网IP地址
    0.0.0.0    ${common_ip_addr_err}

VirtualServers_InternalIP_test_16
    [Documentation]    内网IP地址不为组播地址或保留地址：223 < n1 < 255
    删除所有条目显示成功消息message
    验证内网IP地址    224.0.0.1    ${common_ip_addr_err}
    验证内网IP地址    240.0.0.1    ${common_ip_addr_err}
    修改LAN口IP    223.0.0.1    255.0.0.0
    转到虚拟服务配置页面
    添加条目    wip=223.0.0.2    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400    e_internal_port=600
    ...    message=${common_unlock_save}
    列表第row行第col列是string    1    3    223.0.0.2
    修改LAN口IP    192.168.1.1    255.255.255.0
    转到虚拟服务配置页面
    验证内网IP地址    12.12.12.3    ${common_ip_err}
    [Teardown]    Run Keywords    关闭浏览器    AND    打开浏览器    AND    转到虚拟服务配置页面

VirtualServers_InternalIP_test_17
    [Documentation]    内网IP地址不为回环地址：n1 = 127
    [Template]    验证内网IP地址
    127.0.0.1    ${common_ip_addr_err}

VirtualServers_InternalIP_test_18
    [Documentation]    内网IP地址不为保留地址：n1=0
    [Template]    验证内网IP地址
    0.168.1.10    ${common_ip_addr_err}

VirtualServers_InternalSPort_test_19
    [Documentation]    内部端口的起始端口输入小于1，提示端口不能大于65535或小于1
    [Template]    验证端口
    1000    2000    0    ${EMPTY}    ${common_port_err}

VirtualServers_InternalSPort_test_20
    [Documentation]    内部端口的起始端口输入大于65535，提示端口不能大于65535或小于1
    [Template]    验证端口
    1000    2000    65536    ${EMPTY}    ${common_port_err}

VirtualServers_InternalSPort_test_21
    [Documentation]    内部端口的起始端口输入为非int类字符，提示端口不能含非数字字符
    [Template]    验证端口
    1000    2000    中    ${EMPTY}    ${common_port_err}
    1000    2000    0.1    ${EMPTY}    ${common_port_err}
    1000    2000    @    ${EMPTY}    ${common_port_err}
    1000    2000    a    ${EMPTY}    ${common_port_err}
    1000    2000    A    ${EMPTY}    ${common_port_err}

VirtualServers_InternalEPort_test_22
    [Documentation]    内部端口的结束端口输入小于1，提示端口不能大于65535或小于1
    [Template]    验证端口
    1000    2000    200    0    ${common_port_err}

VirtualServers_InternalEPort_test_23
    [Documentation]    内部端口的结束端口输入大于65535，提示端口不能大于65535或小于1
    [Template]    验证端口
    1000    2000    200    65536    ${common_port_err}

VirtualServers_InternalEPort_test_24
    [Documentation]    内部端口的结束端口输入为非int类字符，提示端口不能含非数字字符
    [Template]    验证端口
    1000    2000    200    中    ${common_port_err}
    1000    2000    200    0.1    ${common_port_err}
    1000    2000    200    @    ${common_port_err}
    1000    2000    200    a    ${common_port_err}
    1000    2000    200    A    ${common_port_err}

VirtualServers_InternalEPort_test_25
    [Documentation]    内部端口的起始端口小于结束端口设置成功
    删除所有条目显示成功消息message
    验证端口    100    200    500    600    ${common_unlock_save}
    列表第row行第col列是string    1    6    500-600

VirtualServers_InternalEPort_test_26
    [Documentation]    内部端口的起始端口等于结束端口设置成功
    删除所有条目显示成功消息message
    验证端口    100    100    500    500    ${common_unlock_save}
    列表第row行第col列是string    1    6    500-500

VirtualServers_InternalEPort_test_27
    [Documentation]    内部端口的超始端口大于结束端口，保存失败，提示“起始端口不能大于结束端口”
    删除所有条目显示成功消息message
    验证端口    100    200    600    500    ${common_cmp_port_err}

VirtualServers_ExternalSPort_test_28
    [Documentation]    外部端口的起始端口输入小于1，提示端口不能大于65535或小于1
    [Template]    验证外部端口
    0    ${EMPTY}    ${common_port_err}

VirtualServers_ExternalSPort_test_29
    [Documentation]    外部端口的起始端口输入大于65535，提示端口不能大于65535或小于1
    [Template]    验证外部端口
    65536    ${EMPTY}    ${common_port_err}

VirtualServers_ExternalSPort_test_30
    [Documentation]    外部端口的起始端口输入为非int类字符，提示端口不能含非数字字符
    [Template]    验证外部端口
    中    ${EMPTY}    ${common_port_err}
    0.1    ${EMPTY}    ${common_port_err}
    @    ${EMPTY}    ${common_port_err}
    a    ${EMPTY}    ${common_port_err}
    A    ${EMPTY}    ${common_port_err}

VirtualServers_ExternalEPort_test_31
    [Documentation]    外部端口的结束端口输入小于1，提示端口不能大于65535或小于1
    [Template]    验证外部端口
    200    0    ${common_port_err}

VirtualServers_ExternalEPort_test_32
    [Documentation]    外部端口的结束端口输入大于65535，提示端口不能大于65535或小于1
    [Template]    验证外部端口
    200    65536    ${common_port_err}

VirtualServers_ExternalEPort_test_33
    [Documentation]    外部端口的结束端口输入为非int类字符，提示端口不能含非数字字符
    [Template]    验证外部端口
    200    中    ${common_port_err}
    200    0.1    ${common_port_err}
    200    @    ${common_port_err}
    200    a    ${common_port_err}
    200    A    ${common_port_err}

VirtualServers_ExternalEPort_test_34
    [Documentation]    外部端口的起始端口小于结束端口设置成功
    删除所有条目显示成功消息message
    验证端口    100    200    500    600    ${common_unlock_save}
    列表第row行第col列是string    1    5    100-200

VirtualServers_ExternalEPort_test_35
    [Documentation]    外部端口的起始端口等于结束端口设置成功
    删除所有条目显示成功消息message
    验证端口    100    100    500    500    ${common_unlock_save}
    列表第row行第col列是string    1    5    100-100

VirtualServers_ExternalEPort_test_36
    [Documentation]    外部端口的超始端口大于结束端口，保存失败，提示“起始端口不能大于结束端口”
    [Template]    验证外部端口
    200    100    ${common_cmp_port_err}

VirtualServers_ADD_test_37
    [Documentation]    成功增加一条条目，自动刷新后存在
    删除所有条目显示成功消息message
    添加条目    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400
    ...    e_internal_port=600    message=${common_unlock_save}
    验证列表中某一行每一列的值    1    abcd    192.168.1.30    TCP    1000-1200    400-600
    #填写数据的模板
    Comment    添加条目    name=    wip=    wvir_proto=    s_external_port=    e_external_port=
    ...    s_internal_port=    e_internal_port=    message=

VirtualServers_ADD_test_38
    [Documentation]    添加一条条目，内容和已添加条目中的一条条目存在以下内容的全部重复：｛内网IP、
    ...    生效协议、外部端口、内部端口｝，提示条目已存在，添加失败。
    删除所有条目显示成功消息message
    添加条目    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400
    ...    e_internal_port=600    message=${common_unlock_save}
    验证列表中某一行每一列的值    1    abcd    192.168.1.30    TCP    1000-1200    400-600
    #再次添加条目
    添加条目    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400
    ...    e_internal_port=600    message=${common_port_occupy_err}
    #填写数据的模板
    Comment    添加条目    name=    wip=    wvir_proto=    s_external_port=    e_external_port=
    ...    s_internal_port=    e_internal_port=    message=

VirtualServers_ADD_test_39
    [Documentation]    超出最大添加条数256，提示无法添加
    删除所有条目显示成功消息message
    : FOR    ${i}    IN RANGE    31    48
    \    ${SIP}    Catenate    SEPARATOR=    192.168.1.    ${i}
    \    Run Keyword If    '${i}'!='47'    验证列表添加的最大条目数    ${SIP}    ${i}    ${common_unlock_save}
    \    Run Keyword If    '${i}'=='47'    验证列表添加的最大条目数    ${SIP}    ${i}    ${common_item_err}
    列表第row行第col列是string    1    3    192.168.1.46

VirtualServers_DELETE_test_40
    [Documentation]    成功删除一条条目，自动刷新后不存在
    删除所有条目显示成功消息message
    添加条目    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400
    ...    e_internal_port=600    message=${common_unlock_save}
    添加条目    wip=192.168.1.25    wvir_proto=TCP    s_external_port=1    e_external_port=10    s_internal_port=1    e_internal_port=10
    ...    message=${common_unlock_save}
    删除列表里第${row}行显示成功消息${message}    1    ${common_unlock_delete}
    验证列表中某一行每一列的值    1    abcd    192.168.1.30    TCP    1000-1200    400-600

VirtualServers_DELETE_test_41
    [Documentation]    成功删除所有条目，自动刷新后不存在
    删除所有条目显示成功消息message
    添加条目    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400
    ...    e_internal_port=600    message=${common_unlock_save}
    添加条目    wip=192.168.1.25    wvir_proto=TCP    s_external_port=1    e_external_port=10    s_internal_port=1    e_internal_port=10
    ...    message=${common_unlock_save}
    删除所有条目显示成功消息message

VirtualServers_MODIFY_test_44
    [Documentation]    成功修改一条条目，自动刷新后改变
    删除所有条目显示成功消息message
    添加条目    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400
    ...    e_internal_port=600    message=${common_unlock_save}
    修改第{row}行条目    1    name=${EMPTY}    wip=192.168.1.40    wvir_proto=UDP    s_external_port=1    e_external_port=10
    ...    s_internal_port=1    e_internal_port=10    message=${common_unlock_save}
    验证列表中某一行每一列的值    1    ${EMPTY}    192.168.1.40    UDP    1-10    1-10

VirtualServers_MODIFY_test_46
    [Documentation]    修改一条条目，内容和已添加条目中的一条条目存在以下内容的全部重复：｛内网IP、生效协议、
    ...    外部端口、内部端口｝，提示条目已存在，修改失败。
    删除所有条目显示成功消息message
    添加条目    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200    s_internal_port=400
    ...    e_internal_port=600    message=${common_unlock_save}
    添加条目    wip=192.168.1.40    wvir_proto=UDP    s_external_port=1    e_external_port=10    s_internal_port=1    e_internal_port=10
    ...    message=${common_unlock_save}
    修改第{row}行条目    1    name=abcd    wip=192.168.1.30    wvir_proto=TCP    s_external_port=1000    e_external_port=1200
    ...    s_internal_port=400    e_internal_port=600    message=${common_port_occupy_err}
    验证列表中某一行每一列的值    2    abcd    192.168.1.30    TCP    1000-1200    400-600
    验证列表中某一行每一列的值    1    ${EMPTY}    192.168.1.40    UDP    1-10    1-10

VirtualServers_InternalSPortMaxlen_test_68
    [Documentation]    *内部端口起始端口超过5个字符后，无法输入*
    [Template]    验证最大输入长度
    vir_inport_start    67890021    5

VirtualServers_InternalEPortMaxlen_test_69
    [Documentation]    *内部端口结束端口超过5个字符后，无法输入*
    [Template]    验证最大输入长度
    vir_inport_end    12121212121    5

VirtualServers_ExternalSPortMaxLen_test_70
    [Documentation]    *外部端口起始端口超过5个字符后，无法输入*
    [Template]    验证最大输入长度
    vir_outport_start    1234566    5

VirtualServers_ExternalEPortMaxLen_test_71
    [Documentation]    *外部端口结束端口超过5个字符后，无法输入*
    [Template]    验证最大输入长度
    vir_outport_end    1232142143    5

VirtualServers_InternalPortandExternalPortRange_test_72
    [Documentation]    *内部端口范围长度（”结束端口号“减去起”始端口号“）不等于外部端口范围长度（”结束端口号“减去起”始端口号“）时，提示错误*
    [Template]    验证端口
    12    12    13    14    ${common_port_len_err}
    13    14    12    12    ${common_port_len_err}

VirtualServers_InternalSPort_test_64
    [Documentation]    *内部起始端口不能为空*
    [Template]    验证端口
    12    12    ${EMPTY}    13    ${common_port_err}

VirtualServers_InternalEPort_test_65
    [Documentation]    *内部结束端口可以为空，起始端口不为空，结束端口为空时，表示内部端口为起始值*
    删除所有条目显示成功消息message
    验证端口    1234    1234    12    ${EMPTY}    ${common_unlock_save}
    列表第row行第col列是string    1    6    12-12

VirtualServers_ExternalSPort_test_66
    [Documentation]    *外部端口起始端口不能为空*
    [Template]
    删除所有条目显示成功消息message
    验证端口    ${EMPTY}    12    13    13    ${common_port_err}

VirtualServers_ExternalEPort_test_67
    [Documentation]    *外部端口结束端口可以为空；当起始端口不为空，而结束端口为空时，表示外部端口为起始端口的值。*
    删除所有条目显示成功消息message
    验证端口    1234    \    12    12    ${common_unlock_save}
    列表第row行第col列是string    1    5    1234-1234

VirtualServers_InternalPortandExternalPortRange_test_73
    [Documentation]    *当内部端口结束端口为空时，外部端口为一个范围长度时，允许保存；*
    ...
    ...    *当外部端口结束端口为空时，内部端口为一个范围长度时，提示错误*
    [Setup]    删除所有条目显示成功消息message
    [Template]    验证端口
    100    120    200    ${EMPTY}    ${common_unlock_save}
    100    ${EMPTY}    300    301    ${common_port_len_err}

*** Keywords ***
验证规则名
    [Arguments]    ${name}    ${message}
    删除所有条目显示成功消息message
    设置虚拟服务参数    des=${name}    ip=192.168.1.30    start_external_port=1000    end_external_port=1100    start_internal_port=1200    end_internal_port=1300
    显示消息    ${message}

删除所有条目显示成功消息message
    Click Element    id=del_virtual_all
    Choose Ok On Next Confirmation
    ${message}    Confirm Action
    Should Be Equal    ${message}    ${common_del_tip}
    显示消息    ${common_unlock_delete}

列表第row行第col列是string
    [Arguments]    ${row}    ${col}    ${string}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    ${col}
    Should Be Equal    ${v}    ${string}

验证最大输入长度
    [Arguments]    ${id}    ${name}    ${max_len}
    验证输入框最大输入长度    ${id}    ${name}    ${max_len}

验证内网IP地址
    [Arguments]    ${wip}    ${message}
    设置虚拟服务参数    ip=${wip}
    显示消息    ${message}

验证协议
    [Arguments]    ${protocol}    ${message}
    删除所有条目显示成功消息message
    设置虚拟服务参数    ip=192.168.1.30    ${protocol}    start_external_port=1000    end_external_port=1100    start_internal_port=1200    end_internal_port=1300
    显示消息    ${message}

验证端口
    [Arguments]    ${s_external_port}    ${e_external_port}    ${s_internal_port}    ${e_internal_port}    ${message}
    设置虚拟服务参数    ip=192.168.1.30    start_external_port=${s_external_port}    end_external_port=${e_external_port}    start_internal_port=${s_internal_port}    end_internal_port=${e_internal_port}
    显示消息    ${message}

验证外部端口
    [Arguments]    ${s_external_port}    ${e_external_port}    ${message}
    设置虚拟服务参数    ip=192.168.1.30    start_external_port=${s_external_port}    end_external_port=${e_external_port}
    显示消息    ${message}

添加条目
    [Arguments]    ${name}=${None}    ${wip}=${None}    ${wvir_proto}=ALL    ${s_external_port}=${None}    ${e_external_port}=${None}    ${s_internal_port}=${None}
    ...    ${e_internal_port}=${None}    ${message}=${None}
    设置虚拟服务参数    des=${name}    ip=${wip}    vir_proto=${wvir_proto}    start_external_port=${s_external_port}    end_external_port=${e_external_port}    start_internal_port=${s_internal_port}
    ...    end_internal_port=${e_internal_port}
    显示消息    ${message}
    Sleep    1

验证列表中某一行每一列的值
    [Arguments]    ${row}    ${desc2}    ${ip3}    ${protocol4}    ${eport5}    ${iport6}
    ${row}    Evaluate    int(${row})+1
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    2
    Should Be Equal    ${v}    ${desc2}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    3
    Should Be Equal    ${v}    ${ip3}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    4
    Should Be Equal    ${v}    ${protocol4}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    5
    Should Be Equal    ${v}    ${eport5}
    ${v}    Get Table Cell    xpath=//table[@class="df_tab"]    ${row}    6
    Should Be Equal    ${v}    ${iport6}

验证列表添加的最大条目数
    [Arguments]    ${wip}    ${i}    ${message}
    ${ri}    Convert To String    ${i}
    设置虚拟服务参数    ip=${wip}    start_external_port=${ri}    end_external_port=${ri}    start_internal_port=${ri}    end_internal_port=${ri}
    显示消息    ${message}

删除列表里第${row}行显示成功消息${message}
    [Arguments]    ${row}    ${message}
    Click Element    xpath=(//div[@id='${${row}-1}'])[2]
    ${message2}    Confirm Action
    Should Be Equal    ${message2}    ${common_del_one_tip}
    显示消息    ${message}

修改第{row}行条目
    [Arguments]    ${row}    ${name}=${None}    ${wip}=${None}    ${wvir_proto}=ALL    ${s_external_port}=${None}    ${e_external_port}=${None}
    ...    ${s_internal_port}=${None}    ${e_internal_port}=${None}    ${message}=${None}
    Click Element    xpath=(//div[@id='${${row}-1}'])[1]
    设置虚拟服务参数    des=${name}    ip=${wip}    vir_proto=${wvir_proto}    start_external_port=${s_external_port}    end_external_port=${e_external_port}    start_internal_port=${s_internal_port}
    ...    end_internal_port=${e_internal_port}
    显示消息    ${message}
    Sleep    5

等待时间
    Sleep    5

修改LAN口IP
    [Arguments]    ${ip}    ${mask}
    Choose Ok On Next Confirmation
    转到内网配置页面
    设置LAN口IP和MASK    ${ip}    ${mask}
    ${message}    Confirm Action
    Should Be Equal    ${message}    ${common_reboot_tip}
    Sleep    40s
    关闭浏览器
    Open Browser    http://${login_user}:${login_passwd}@${ip}/index.htm    ${browser}
    Sleep    5
